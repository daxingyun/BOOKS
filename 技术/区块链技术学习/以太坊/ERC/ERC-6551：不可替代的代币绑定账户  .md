# ERC-6551：不可替代的代币绑定账户  
ERC-721 代币拥有的智能合约账户的接口和注册表
## 抽象的
该提案定义了一个系统，为每个 ERC-721 代币提供一个智能合约帐户。这些代币绑定账户允许 ERC-721 代币拥有资产并与应用程序交互，而无需更改现有的 ERC-721 智能合约或基础设施。
### 动机
ERC-721 标准使得不可替代代币应用程序出现爆炸式增长。一些著名的用例包括可繁殖的猫、生成艺术品和流动性头寸。

不可替代的代币正日益成为链上身份的一种形式。这很自然地遵循 ERC-721 规范——每个不可替代的代币都有一个全局唯一的标识符，并且通过扩展，也有一个唯一的身份。

与其他形式的链上身份不同，ERC-721 代币不能充当代理或与其他链上资产关联。这种限制与现实世界中许多不可替代资产的实例形成鲜明对比。例如：

- 角色扮演游戏中的角色，根据他们所采取的行动，随着时间的推移积累资产和能力
- 由许多可替代和不可替代组件组成的汽车
- 由多种可替代资产组成的自动化投资组合
- 一张打卡会员卡，允许进入某个场所并记录过去的互动历史

一些提案试图赋予 ERC-721 代币拥有资产的能力。这些提案中的每一个都定义了 ERC-721 标准的扩展。这要求智能合约作者在其 ERC-721 代币合约中包含提案支持。因此，这些提案在很大程度上与之前部署的 ERC-721 合约不兼容。

该提案赋予每个 ERC-721 代币以太坊账户的全部功能，同时保持与之前部署的 ERC-721 代币合约的向后兼容性。它通过无许可注册表，为每个 ERC-721 代币部署唯一的、确定性寻址的智能合约帐户来实现这一点。

每个代币绑定账户都由一个 ERC-721 代币拥有，允许代币与区块链交互、记录交易历史并拥有链上资产。每个代币绑定账户的控制权都委托给 ERC-721 代币的所有者，允许所有者代表其代币发起链上操作。

代币绑定帐户与几乎所有支持以太坊帐户的现有基础设施兼容，从链上协议到链外索引器。代币绑定账户可以拥有任何类型的链上资产，并且可以扩展以支持未来创建的新资产类型。

### 规格
关键词“MUST”, “MUST NOT”, “REQUIRED”, “SHALL”, “SHALL NOT”, “SHOULD”, “SHOULD NOT”, “RECOMMENDED”, “NOT RECOMMENDED”, “MAY”, 和 “OPTIONAL” 本文档中的“”应按照 RFC 2119 和 RFC 8174 中的描述进行解释。
#### 概述
本提案中概述的系统有两个主要组成部分：

- 用于部署代币绑定帐户的无许可注册表
- 代币绑定帐户实现的标准接口

下图说明了 ERC-721 代币、ERC-721 代币所有者、代币绑定账户和注册表之间的关系：

![](./pic/diagram.webp)

#### 注册表
注册表希望充当使用代币绑定帐户的项目的单一入口点。它有两个功能：

- `createAccountimplementation` - 为给定地址的 ERC-721 代币部署代币绑定账户
- `account` - 一个只读函数，用于计算给定地址的 ERC-721 代币的代币绑定账户 `implementation` 地址

注册中心应将每个代币绑定帐户部署为 [ERC-1167](https://eips.ethereum.org/EIPS/eip-1167) 最小代理，并将不可变的常量数据附加到字节码。

每个代币绑定帐户的部署字节码应具有以下结构：

	ERC-1167 Header               (10 bytes)
	<implementation (address)>    (20 bytes)
	ERC-1167 Footer               (15 bytes)
	<salt (uint256)>              (32 bytes)
	<chainId (uint256)>           (32 bytes)
	<tokenContract (address)>     (32 bytes)
	<tokenId (uint256)>           (32 bytes)
`0xbebebebebebebebebebebebebebebebebebebebe` 例如，具有实现地址、salt `0`、链 ID `1`、代币合约 `0xcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcf` 和代币 ID 的代币绑定帐户 `123` 将具有以下部署的字节码：

	363d3d373d3d3d363d73bebebebebebebebebebebebebebebebebebebebe5af43d82803e903d91602b57fd5bf300000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000001000000000000000000000000cfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcfcf000000000000000000000000000000000000000000000000000000000000007b

每个代币绑定帐户代理应将执行委托给实现该 `IERC6551 Account `接口的合约。

注册合约是无需许可的、不可变的，并且没有所有者。注册表的完整源代码可以在下面的 [注册表实现](https://eips.ethereum.org/EIPS/eip-6551#registry-implementation) 部分中找到。注册表应使用 Nick's Factory ( `0x4e59b44847b379578588920cA78FbF26c0B4956C`) 和 salt `0x6551655165516551655165516551655165516551655165516551655165516551 ` 并部署在地址 `TBD` 处。

注册中心应使用 `create2` 操作码部署所有代币绑定账户合约，以便每个 ERC-721 代币的账户地址具有确定性。每个 ERC-721 代币的账户地址应源自实现地址、代币合约地址、代币 ID、EIP-155 链 ID 和可选 salt 的唯一组合。

注册管理机构应实现以下接口：

	interface IERC6551Registry {
	    /// @dev 注册中心应在成功创建帐户时发出 AccountCreated 事件
	    event AccountCreated(
	        address account,
	        address implementation,
	        uint256 chainId,
	        address tokenContract,
	        uint256 tokenId,
	        uint256 salt
	    );
	
	      /// @dev为ERC-721代币创建一个代币绑定帐户。
		///
		///如果帐户已经创建，返回帐户地址而不调用 create2。
		///
		///如果 initData 不为空且 account 尚未创建，则在创建后使用提供的 initData 调用 account。
		///
		///触发 AccountCreated 事件。
		///
		/// @返回帐户的地址
	    function createAccount(
	        address implementation,
	        uint256 chainId,
	        address tokenContract,
	        uint256 tokenId,
	        uint256 salt,
	        bytes calldata initData
	    ) external returns (address);
	
	      /// @dev返回代币绑定帐户的计算地址
		///
		/// @return帐户的计算地址
	    function account(
	        address implementation,
	        uint256 chainId,
	        address tokenContract,
	        uint256 tokenId,
	        uint256 salt
	    ) external view returns (address);
	}
#### 账户接口
所有代币绑定帐户都应该通过注册表创建。

所有代币绑定帐户实现必须实现 ERC-165 接口检测。

所有代币绑定帐户实现都必须实现 ERC-1271 签名验证。

所有代币绑定帐户实现必须实现以下接口：

    /// @dev 这个接口的ERC-165标识符是“0x400a0398”。
    
    interface IERC6551Account {
    
    /// @dev 代笔绑定帐户必须实现' receive '函数。
    ///
    /// 代币绑定账户可以执行任意逻辑来限制接收以太币的条件。
    
    receive() external payable;

    /// @dev 对地址' to '执行' call '，值' value '和 calldata ' data '。
    ///
    /// 如果调用失败，必须恢复并弹出错误。
    ///
    /// 默认情况下，代币绑定帐户必须允许拥有该帐户的 ERC-721 代币的所有者使用 ' executeCall ' 执行任意调用。
    ///
    /// 代币绑定帐户可以实现额外的授权机制，限制 ERC-721 代币持有者执行调用的能力。
    ///
    /// 代币绑定账户可以实现额外的执行功能，向其他非所有者账户授予执行权限。
    ///
    /// @return 调用的结果
    
    function executeCall(
        address to,
        uint256 value,
        bytes calldata data
    ) external payable returns (bytes memory);

    /// @dev 返回拥有该帐户的 ERC-721代币的标识符
    ///
    /// 这个函数的返回值必须是常量——它不能随时间改变。
    ///
    /// @return chainId ERC-721 代币所在链的 EIP-155 ID
    /// @return tokenContract ERC-721 代币的合约地址
    /// @return tokenId ERC-721 代币 ID
    
    function token()
        external
        view
        returns (
            uint256 chainId,
            address tokenContract,
            uint256 tokenId
        );

    /// @dev 返回ERC-721令牌的所有者，如果令牌存在，该令牌控制帐户。
    ///
    /// 该值是通过调用 ERC-721 合同上的 “ownerOf” 获得的。
    ///
    /// @return ERC-721 令牌所有者的地址，该令牌拥有帐户函数 owner() 外部视图返回(地址);
    /// @dev 返回在每次成功事务时更新的 nonce 值
    ///
    /// @return 当前账号 nonce 
    function nonce() external view returns (uint256);
    }
    
## 基本原理
### 反事实账户地址
通过指定规范的帐户注册表，希望支持此提案的应用程序可以在部署该帐户的合约之前使用特定的实现来计算给定代币绑定帐户的地址。这使得资产可以安全地发送给代币所有者，而无需知道所有者的地址。规范注册表还允许客户端应用程序从单个入口点查询代币拥有的资产。
### 账户歧义
上述提出的规范允许 ERC-721 代币拥有多个代币绑定账户，每个实现地址一个。在该提案的开发过程中，考虑了替代架构，该架构将为每个 ERC-721 代币分配一个代币绑定帐户，从而使每个代币绑定帐户地址成为一个明确的标识符。

然而，这些替代方案存在一些权衡。

- 首先

	由于智能合约的无需许可的性质，不可能对每个 ERC-721 代币强制执行一个代币绑定账户的限制。任何希望使用每个 ERC-721 代币的多个代币绑定帐户的人都可以通过部署额外的注册表合约来实现。
- 其次

	将每个 ERC-721 代币限制为单个代币绑定帐户将需要在此提案中包含静态、可信的帐户实现。这种实现将不可避免地对代币绑定账户的功能施加特定的限制。考虑到该提案所支持的未开发用例的数量以及多样化账户实现可能给不可替代代币生态系统带来的好处，作者认为在该提案中定义规范且受约束的实现还为时过早。
- 最后

	该提案旨在授予 ERC-721 代币充当链上代理的能力。在当前实践中，链上代理通常使用多个账户。一个常见的例子是个人使用“热”帐户进行日常使用，使用“冷”帐户来存储贵重物品。如果链上代理通常使用多个账户，那么按理说 ERC-721 代币应该继承相同的能力。

### 代理实施
ERC-1167 最小代理得到现有基础设施的良好支持并且是一种常见的智能合约模式。该提案使用自定义 ERC-1167 代理实现来部署每个代币绑定帐户，该代理实现将盐、实现地址、链 ID、代币合约地址和代币 ID 作为附加到合约字节码的 ABI 编码常量数据进行存储。这允许代币绑定帐户实现轻松查询此数据，同时确保其保持不变。采取这种方法是为了最大限度地提高与现有基础设施的兼容性，同时也为智能合约开发人员在创建自定义代币绑定帐户实现时提供充分的灵活性。
### EIP-155 支持 
该提案使用 EIP-155 链 ID 来识别 ERC-721 代币及其合约地址和代币 ID。ERC-721 代币标识符在单个以太坊链上是全局唯一的，但在多个以太坊链上可能不是唯一的。使用链 ID 来唯一标识 ERC-721 代币，允许希望实施此提案的智能合约作者有选择地支持多链代币绑定账户。
## 向后兼容性
该提案旨在最大程度地向后兼容现有的不可替代代币合约。因此，它不会扩展 ERC-721 标准。

此外，该提案不要求注册表在创建账户之前执行 ERC-165 接口检查以确保 ERC-721 兼容性。这是设计使然，目的是为了最大限度地向后兼容早于 ERC-721 标准的不可替代代币合约，例如 Cryptokitties。实施此提案的智能合约作者可以选择强制执行 ERC-721 的接口检测。

不实现 `ownerOf` 方法的不可替代代币合约（例如 Cryptopunks）与此提案不兼容。本提案中概述的系统可用于支持此类集合，只需进行少量修改，但这超出了本提案的范围。

## 参考实现
### 帐户实施示例

	pragma solidity ^0.8.13;
	
	import "openzeppelin-contracts/utils/introspection/IERC165.sol";
	import "openzeppelin-contracts/token/ERC721/IERC721.sol";
	import "openzeppelin-contracts/interfaces/IERC1271.sol";
	import "openzeppelin-contracts/utils/cryptography/SignatureChecker.sol";
	import "sstore2/utils/Bytecode.sol";
	
	contract ExampleERC6551Account is IERC165, IERC1271, IERC6551Account {
	    receive() external payable {}
	
	    function executeCall(
	        address to,
	        uint256 value,
	        bytes calldata data
	    ) external payable returns (bytes memory result) {
	        require(msg.sender == owner(), "Not token owner");
	
	        bool success;
	        (success, result) = to.call{value: value}(data);
	
	        if (!success) {
	            assembly {
	                revert(add(result, 32), mload(result))
	            }
	        }
	    }
	
	    function token()
	        external
	        view
	        returns (
	            uint256 chainId,
	            address tokenContract,
	            uint256 tokenId
	        )
	    {
	        uint256 length = address(this).code.length
	        return
	            abi.decode(
	                Bytecode.codeAt(address(this), length - 0x60, length),
	                (uint256, address, uint256)
	            );
	    }
	
	    function owner() public view returns (address) {
	        (uint256 chainId, address tokenContract, uint256 tokenId) = this
	            .token();
	        if (chainId != block.chainid) return address(0);
	
	        return IERC721(tokenContract).ownerOf(tokenId);
	    }
	
	    function supportsInterface(bytes4 interfaceId) public pure returns (bool) {
	        return (interfaceId == type(IERC165).interfaceId ||
	            interfaceId == type(IERC6551Account).interfaceId);
	    }
	
	    function isValidSignature(bytes32 hash, bytes memory signature)
	        external
	        view
	        returns (bytes4 magicValue)
	    {
	        bool isValid = SignatureChecker.isValidSignatureNow(
	            owner(),
	            hash,
	            signature
	        );
	
	        if (isValid) {
	            return IERC1271.isValidSignature.selector;
	        }
	
	        return "";
	    }
	}
### 注册实施
	pragma solidity ^0.8.13;
	
	import "openzeppelin-contracts/utils/Create2.sol";
	
	contract ERC6551Registry is IERC6551Registry {
	    error InitializationFailed();
	
	    function createAccount(
	        address implementation,
	        uint256 chainId,
	        address tokenContract,
	        uint256 tokenId,
	        uint256 salt,
	        bytes calldata initData
	    ) external returns (address) {
	        bytes memory code = _creationCode(implementation, chainId, tokenContract, tokenId, salt);
	
	        address _account = Create2.computeAddress(
	            bytes32(salt),
	            keccak256(code)
	        );
	
	        if (_account.code.length != 0) return _account;
	
	        _account = Create2.deploy(0, bytes32(salt), code);
	
	        if (initData.length != 0) {
	            (bool success, ) = _account.call(initData);
	            if (!success) revert InitializationFailed();
	        }
	
	        emit AccountCreated(
	            _account,
	            implementation,
	            chainId,
	            tokenContract,
	            tokenId,
	            salt
	        );
	
	        return _account;
	    }
	
	    function account(
	        address implementation,
	        uint256 chainId,
	        address tokenContract,
	        uint256 tokenId,
	        uint256 salt
	    ) external view returns (address) {
	        bytes32 bytecodeHash = keccak256(
	            _creationCode(implementation, chainId, tokenContract, tokenId, salt)
	        );
	
	        return Create2.computeAddress(bytes32(salt), bytecodeHash);
	    }
	
	    function _creationCode(
	        address implementation_,
	        uint256 chainId_,
	        address tokenContract_,
	        uint256 tokenId_,
	        uint256 salt_
	    ) internal pure returns (bytes memory) {
	        return
	            abi.encodePacked(
	                hex"3d60ad80600a3d3981f3363d3d373d3d3d363d73",
	                implementation_,
	                hex"5af43d82803e903d91602b57fd5bf3",
	                abi.encode(salt_, chainId_, tokenContract_, tokenId_)
	            );
	    }
	}
## 安全考虑
### 预防诈骗
为了实现代币绑定账户的去信任销售，去中心化市场需要实施保护措施，防止恶意账户所有者的欺诈行为。

考虑以下潜在的骗局：

- Alice 拥有 ERC-721 代币 X，该代币拥有代币绑定账户 Y。
- Alice 将 10ETH 存入账户 Y
- Bob 提出通过去中心化市场以 11 ETH 的价格购买代币 X，假设他将收到账户 Y 中存储的 10ETH 以及代币
- Alice 从代币绑定账户中提取 10ETH，并立即接受 Bob 的报价
- Bob 收到代币X，但账户Y为空

为了减少恶意账户所有者的欺诈行为，去中心化市场应该在市场层面实施针对此类诈骗的保护措施。实施此 EIP 的合约还可以实施某些针对欺诈行为的保护措施。

以下是一些需要考虑的缓解策略：

- 将当前代币绑定账户随机数附加到市场订单中。如果自下订单以来帐户的随机数发生了变化，则认为报价无效。此功能需要在市场级别得到支持。
- 资产承诺列表附加到市场订单中，这些资产承诺在订单履行时预计将保留在代币绑定账户中。如果自下订单以来任何承诺资产已从账户中删除，则视为该报价无效。这也需要由市场来实施。
- 通过外部智能合约将订单提交到去中心化市场，该智能合约在验证订单签名之前执行上述逻辑。这允许在没有市场支持的情况下实现安全转移。
在代币绑定帐户实现上实施锁定机制，防止恶意所有者在锁定时从帐户中提取资产
防止欺诈不属于本提案的范围。

## 所有权周期
如果创建所有权周期，代币绑定账户中持有的所有资产可能会变得无法访问。最简单的例子是 ERC-721 代币被转移到其自己的代币绑定账户的情况。如果发生这种情况，ERC-721 代币和存储在代币绑定账户中的所有资产都将永久无法访问，因为代币绑定账户无法执行转移 ERC-721 代币的交易。

所有权周期可以在 n>0 代币绑定账户的任何图中引入。鉴于所需的无限搜索空间，对这些循环的链上预防很难执行，因此超出了本提案的范围。鼓励希望采用此提案的应用程序客户和帐户实施者实施限制所有权周期可能性的措施。

## 参考
- [ERC-6551: Non-fungible Token Bound Accounts](https://eips.ethereum.org/EIPS/eip-6551)
- [https://docs.tokenbound.org/](https://docs.tokenbound.org/)