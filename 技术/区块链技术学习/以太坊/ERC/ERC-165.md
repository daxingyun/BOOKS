# ERC-165 标准接口检测
## 简单总结
创建一个标准方法来发布和检测智能合约实现的接口
## 抽象
标准化以下内容：

- 如何识别接口
- 合约将如何发布它实现的接口
- 如何检测合约是否实施 ERC-165
- 如何检测合约是否实现了任何给定的接口
 
##  动机
对于一些像 [ERC-20 ](https://eips.ethereum.org/EIPS/eip-20) 代币接口这样的“标准接口” ，有时需要查询合约是否支持该接口，如果支持，该接口是哪个版本的，以适应合约交互的方式。特别是对于 ERC-20，已经提出了版本标识符。该提案规范了接口的概念，规范了接口的标识（命名）。
##  规格
###  如何识别接口
对于此标准，接口是一组由 [Ethereum ABI](https://solidity.readthedocs.io/en/develop/abi-spec.html#function-selector) 定义的函数选择器。这是 [Solidity 的接口概念](https://solidity.readthedocs.io/en/develop/abi-spec.html)和 `interface` 关键字定义的一个子集，它也定义了返回类型、可变性和事件。

将接口标识符定义为接口中所有函数选择器的 XOR。此代码示例显示了如何计算接口标识符：

	pragma solidity ^0.4.20;
	
	interface Solidity101 {
	    function hello() external pure;
	    function world(int) external pure;
	}
	
	contract Selector {
	    function calculateSelector() public pure returns (bytes4) {
	        Solidity101 i;
	        return i.hello.selector ^ i.world.selector;
	    }
	}
注意：接口不允许可选功能，因此，接口标识将不包括它们。

合约将如何发布它实现的接口

符合 ERC-165 的合约应实现以下接口（简称 `ERC165.sol`）：

	pragma solidity ^0.4.20;
	
	interface ERC165 {
	   /// @notice 查询是否合约实现接口
	   /// @param indefitionId 接口标识符，如 erc-165 所示
	   /// @dev 界面标识在 ERC-165 中指定。 这个功能使用少于 30,000 GAS。
	   /// @return 如果合约实现 `InterfaceId` 和 `verionid` 不是 0xffffffff，则为 `ture` ，否则为 `false` 
	   
	    function supportsInterface(bytes4 interfaceID) external view returns (bool);
	}
此接口的接口标识符为 `0x01ffc9a7`。可以通过运行 `bytes4(keccak256('supportsInterface(bytes4)'));` 或使用 `Selector` 上面的合约来计算。

因此，执行合约将有一个 `supportsInterface` 函数返回：

- 当 `interfaceID` 是 `0x01ffc9a7`（EIP165接口）则为 `true` 
- 当 `interfaceID` 是 `0xffffffff` 则为 `false`
- 对于 `interfaceID` 本合约执行的任何其他为 `true`
- 对于任何其他 `interfaceID` 为 `false`

此函数必须返回一个布尔值并最多使用 30,000 气体。

实现说明，有几种逻辑方式来实现这个功能。请参阅示例实现和有关 gas 使用的讨论。

如何检测合约是否实施 ERC-165

- 源合约 `STATICCALL` 使用输入 `data:0x01ffc9a701ffc9a700000000000000000000000000000000000000000000000000000000` 和 gas 30,000到目标地址。这对应于 `contract.supportsInterface(0x01ffc9a7)`。
	- 如果调用失败或返回 `false`，则目标合约未实施 ERC-165。
	- 如果调用返回 `true`，则使用输入数据进行第二次调用 `0x01ffc9a7ffffffff00000000000000000000000000000000000000000000000000000000`。
		- 如果第二次调用失败或返回 `true`，则目标合约未实现 ERC-165。
		- 否则它实现 ERC-165。
 
如何检测合约是否实现了任何给定的接口

- 如果不确定合约是否执行 ERC-165，请使用上述程序进行确认。
- 如果它没有实现 ERC-165，那么你将不得不看看它使用了哪些老式的方法。
- 如果它实现了 ERC-165，那么只需调用 `supportsInterface(interfaceID)` 以确定它是否实现了您可以使用的接口。
 
## 基本原理
我们试图让这个规范尽可能简单。此实现也与当前的 Solidity 版本兼容。
## 向后兼容
上面描述的机制（带有 `0xffffffff`）应该适用于本标准之前的大多数合约，以确定它们没有实施 ERC-165。

另外的 [ENS](https://eips.ethereum.org/EIPS/eip-137) 已经实现了这个 EIP。
## 测试用例
以下是检测其他合约实现哪些接口的合约。来自 @fulldecent 和@jbaylina。

	pragma solidity ^0.4.20;
	
	contract ERC165Query {
	    bytes4 constant InvalidID = 0xffffffff;
	    bytes4 constant ERC165ID = 0x01ffc9a7;
	
	    function doesContractImplementInterface(address _contract, bytes4 _interfaceId) external view returns (bool) {
	        uint256 success;
	        uint256 result;
	
	        (success, result) = noThrowCall(_contract, ERC165ID);
	        if ((success==0)||(result==0)) {
	            return false;
	        }
	
	        (success, result) = noThrowCall(_contract, InvalidID);
	        if ((success==0)||(result!=0)) {
	            return false;
	        }
	
	        (success, result) = noThrowCall(_contract, _interfaceId);
	        if ((success==1)&&(result==1)) {
	            return true;
	        }
	        return false;
	    }
	
	    function noThrowCall(address _contract, bytes4 _interfaceId) constant internal returns (uint256 success, uint256 result) {
	        bytes4 erc165ID = ERC165ID;
	
	        assembly {
	                let x := mload(0x40)               //使用 “免费内存指针” 找到空存储位置
	                mstore(x, erc165ID)                //在空存储开始时签名
	                mstore(add(x, 0x04), _interfaceId) // 将第一个参数直接放在签名旁边
	
	                success := staticcall(
	                                    30000,         // 30k gas
	                                    _contract,     // To 地址
	                                    x,             // 输入存储在位置x上
	                                    0x24,          // 输入长36字节
	                                    x,             // 存储输出超过输入（节省空间）
	                                    0x20)          // 输出长32个字节
	
	                result := mload(x)                 // 加载结果
	        }
	    }
	}

## 执行
这种方法使用的 `view` 函数实现 `supportsInterface`。任何输入的执行成本都是 586 gas。但是合约初始化需要存储每个接口（`SSTORE` 是 20,000 gas）。该 `ERC165MappingImplementation` 合约是通用的且可重用的。

	pragma solidity ^0.4.20;
	
	import "./ERC165.sol";
	
	contract ERC165MappingImplementation is ERC165 {
	    /// @dev 您不得将元素 0xffffffff 设置为 true
	    mapping(bytes4 => bool) internal supportedInterfaces;
	
	    function ERC165MappingImplementation() internal {
	        supportedInterfaces[this.supportsInterface.selector] = true;
	    }
	
	    function supportsInterface(bytes4 interfaceID) external view returns (bool) {
	        return supportedInterfaces[interfaceID];
	    }
	}
	
	interface Simpson {
	    function is2D() external returns (bool);
	    function skinColor() external returns (string);
	}
	
	contract Lisa is ERC165MappingImplementation, Simpson {
	    function Lisa() public {
	        supportedInterfaces[this.is2D.selector ^ this.skinColor.selector] = true;
	    }
	
	    function is2D() external returns (bool){}
	    function skinColor() external returns (string){}
	}
以下是一个 `pure` 功能的实现 `supportsInterface`。最坏情况下的执行成本是 236 gas，但随着支持的接口数量的增加而线性增加。

	pragma solidity ^0.4.20;
	
	import "./ERC165.sol";
	
	interface Simpson {
	    function is2D() external returns (bool);
	    function skinColor() external returns (string);
	}
	
	contract Homer is ERC165, Simpson {
	    function supportsInterface(bytes4 interfaceID) external view returns (bool) {
	        return
	          interfaceID == this.supportsInterface.selector || // ERC165
	          interfaceID == this.is2D.selector
	                         ^ this.skinColor.selector; // Simpson
	    }
	
	    function is2D() external returns (bool){}
	    function skinColor() external returns (string){}
	}
通过三个或更多支持的接口（包括 ERC165 本身作为所需的支持接口），映射方法（在每种情况下）比纯方法（在最坏的情况下）花费更少的 gas。

## 版本历史
- PR 1640，最终确定 2019-01-23 – 这更正了 noThrowCall 测试用例以使用 36 个字节而不是之前的 32 个字节。之前的代码是一个错误，它仍然在 Solidity 0.4.x 中默默工作，但被 Solidity 0.5.0 中引入的新行为打破了。此更改已在 [#1640](https://github.com/ethereum/EIPs/pull/1640) 中讨论。
- EIP 165，最终确定 2018-04-20 – 原始发布版本。

## 版权
通过 [CC0](https://creativecommons.org/publicdomain/zero/1.0/) 放弃版权和相关权。