# EIP-1155 多代币标准
## 简单总结
管理多种代币类型的合约的标准接口。单个部署的合约可以包括可分割代币、不了分割代币或其他配置（例如半可替换代币）的任意组合。
## 抽象的
该标准概述了一个智能合约接口，可以代表任意数量的可分割和不可分割代币类型。

- ERC-20 等现有标准要求为每种代币类型部署单独的合约。
- ERC-721 标准的代币 ID 是一个单一的不可分割的索引，这些不可分割的组被部署为具有整个集合设置的单一合约。

相比之下，ERC-1155 多代币标准允许每个令牌 ID 代表一个新的可配置令牌类型，该类型可能具有自己的元数据、供应和其他属性。

所述 `_id` 包含在每个函数的参数集的参数指示一个特定的标记或标记类型中的事务。

## 动机
ERC-20 和 ERC-721 等代币标准要求为每种代币类型或集合部署单独的合约。这在以太坊区块链上放置了大量冗余字节码，并通过将每个代币合约分离到其自己的许可地址的性质来限制某些功能。随着区块链游戏和 Enjin Coin 等平台的兴起，游戏开发者可能会创建数以千计的代币类型，需要一种新型的代币标准来支持它们。而 ERC-1155 也非特定于游戏，许多其他应用程序都可以从这种灵活性中受益。

这种设计可以实现新功能，例如一次传输多种代币类型，从而节省交易成本。多个代币的交易（托管/原子交换）可以建立在这个标准之上，并且不需要单独“批准”单个代币合约。在单个合约中描述和混合多种可分割或不可分割的代币类型也很容易。
## 规格
本文档中的关键词 “MUST”, “MUST NOT”, “REQUIRED”, “SHALL”, “SHALL NOT”, “SHOULD”, “SHOULD NOT”, “RECOMMENDED”, “MAY” 和 “OPTIONAL” 是按照 [RFC 2119](https://datatracker.ietf.org/doc/html/rfc2119) 中的描述进行解释。

### ERC-1155 标准的智能合约
- 实现 ERC-1155 标准的智能合约必须实现 `ERC1155` 接口中的所有功能。
- 实现 ERC-1155 标准的智能合约必须实现 ERC-165 `supportsInterface` 函数并且 `true` 如果 `0xd9b67a26` 通过`interfaceID` 参数传递，则必须返回常量值。
- 标准代码

		pragma solidity ^0.5.9;
		
		/**
		    @title ERC-1155 Multi Token Standard
		    //ERC-1155多代币标准
		    
		    @dev See https://eips.ethereum.org/EIPS/eip-1155
		    Note: The ERC-165 identifier for this interface is 0xd9b67a26.
		    // 注意:这个 ERC-165 接口的标识符是 0xd9b67a26。
		 */
		 
		interface ERC1155 /* is ERC165 */ {
		
		    /**
		        @dev Either `TransferSingle` or `TransferBatch` MUST emit when tokens are transferred, including zero value transfers as well as minting or burning (see "Safe Transfer Rules" section of the standard).
		        // 当 token 被转移时，' TransferSingle '或' TransferBatch '必须被释放，包括零值传输以及铸造或燃烧(参见标准的 "Safe Transfer Rules" 部分)。
		        The `_operator` argument MUST be the address of an account/contract that is approved to make the transfer (SHOULD be msg.sender).
		        // ' _operator '参数必须是被批准进行转账的账户/合同的地址 (应该是 msg.sender)。
		        The `_from` argument MUST be the address of the holder whose balance is decreased.
		        // 参数' _from '必须是余额减少的持有者的地址。也就是代币发送者
		        The `_to` argument MUST be the address of the recipient whose balance is increased.
		        // ' _to '参数必须是余额增加的接收者的地址。。也就是代币接收者
		        The `_id` argument MUST be the token type being transferred.
		        // 参数' _id '必须是要传输的代币类型。
		        The `_value` argument MUST be the number of tokens the holder balance is decreased by and match what the recipient balance is increased by.
		        // ' _value '参数必须是持有方余额减少并匹配接收方余额增加的代币数量。
		        When minting/creating tokens, the `_from` argument MUST be set to `0x0` (i.e. zero address).
		        // 当创建/创建代币时，`_from`参数必须设置为`0x0 '(即零地址)。
		        When burning/destroying tokens, the `_to` argument MUST be set to `0x0` (i.e. zero address).  
		        // 当燃烧/销毁代币时，' _to '参数必须设置为' 0x0 '(即零地址)。    
		    */
		    
		    event TransferSingle(address indexed _operator, address indexed _from, address indexed _to, uint256 _id, uint256 _value);
		
		    /**
		        // 当 token 被转移时，' TransferSingle '或' TransferBatch '必须被释放，包括零值传输以及铸造或燃烧(参见标准的 "Safe Transfer Rules" 部分)。    
		        // ' _operator '参数必须是被批准进行转账的账户/合同的地址 (应该是 msg.sender)。
		        // 参数' _from '必须是余额减少的持有者的地址。也就是代币发送者
		         // ' _to '参数必须是余额增加的接收者的地址。。也就是代币接收者
		        The `_ids` argument MUST be the list of tokens being transferred.
		        // 参数' _ids '必须是正在传输的代币列表
		        The `_values` argument MUST be the list of number of tokens (matching the list and order of tokens specified in _ids) the holder balance is decreased by and match what the recipient balance is increased by.
		        // ' _values '参数必须是代币数量的列表(匹配 _ids 中指定的代币列表和顺序)，持有者余额减少，并匹配接收者余额增加。
		        //当创建/创建代币时，`_from`参数必须设置为`0x0 '(即零地址)。
		        //当燃烧/销毁代币时，' _to '参数必须设置为' 0x0 '(即零地址)。               
		    */
		    
		    event TransferBatch(address indexed _operator, address indexed _from, address indexed _to, uint256[] _ids, uint256[] _values);
		
		    /**
		        @dev MUST emit when approval for a second party/operator address to manage all tokens for an owner address is enabled or disabled (absence of an event assumes disabled).  
		        @dev 必须在允许或禁用第二方/操作员地址管理所有者地址的所有令牌时发出(如果没有事件则认为是禁用的)。      
		    */
		    
		    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);
		
		    /**
		        @dev MUST emit when the URI is updated for a token ID.
		        // 必须在更新代币 ID 的 URI 时发出。
		        URIs are defined in RFC 3986.
		        // uri 在 RFC 3986 中定义。
		        The URI MUST point to a JSON file that conforms to the "ERC-1155 Metadata URI JSON Schema".
		        // URI 必须指向一个符合“ERC-1155元 数据 URI JSON Schema” 的JSON文件。
		    */
		    event URI(string _value, uint256 indexed _id);
		
		    /**
		        @notice Transfers `_value` amount of an `_id` from the `_from` address to the `_to` address specified (with safety call).
		        // 将 _id 的 “_value”从 “_from” 地址转移到指定的 “_to” 地址(使用 safety call )。
		        
		        @dev Caller must be approved to manage the tokens being transferred out of the `_from` account (see "Approval" section of the standard).
		        //调用者必须经过批准才能管理从 ' _from ' 账户转移出去的代币(参见标准的“Approval”部分)。
		        MUST revert if `_to` is the zero address.
		        // 如果 ' _to ' 是零地址必须恢复。
		        MUST revert if balance of holder for token `_id` is lower than the `_value` sent.
		        // 如果代币 ' _id ' 的持有者的余额低于发送的 ' _value '，必须恢复。
		        MUST revert on any other error.
		        // 在出现其他错误时必须恢复。
		        MUST emit the `TransferSingle` event to reflect the balance change (see "Safe Transfer Rules" section of the standard).
		        // 必须发出 ' TransferSingle ' 事件以反映余额变化(请参阅标准的“Safe Transfer Rules”部分)。
		        After the above conditions are met, this function MUST check if `_to` is a smart contract (e.g. code size > 0). If so, it MUST call `onERC1155Received` on `_to` and act appropriately (see "Safe Transfer Rules" section of the standard).     
		        // 在满足上述条件后，这个函数必须检查 ' _to ' 是否为一个智能合约(例如代码大小为> 0)。如果是，它必须在 ' _to ' 上调用 ' onERC1155Received '，并采取适当的行动(参见标准的“Safe Transfer Rules”部分)。
		       
		        @参数     
		        	- _from    _from 源地址
		        	- _to    _to 目标地址
		        	-  _id     ID 代币类型
		        	- _value   转账金额
		       	- _data  没有指定格式的附加数据，必须在调用 `_to` 上的 `onERC1155Received` 时不变地发送	    */ 
		    
		    function safeTransferFrom(address _from, address _to, uint256 _id, uint256 _value, bytes calldata _data) external;
		
		    /**
		        @notice Transfers `_values` amount(s) of `_ids` from the `_from` address to the `_to` address specified (with safety call).
		        //将 `_values` 数量的 `_ids` 从 `_from` 地址转移到指定的 `_to` 地址（使用 safety call）。
		        
		        @dev 调用者必须获得批准才能管理从 `_from` 帐户转出的令牌（请参阅标准的“Approval”部分）
		        //如果 ' _to ' 是零地址必须恢复。
		        MUST revert if length of `_ids` is not the same as length of `_values`.
		       // 如果`_ids` 的长度与`_values` 的长度不同，则必须恢复。
		        MUST revert if any of the balance(s) of the holder(s) for token(s) in `_ids` is lower than the respective amount(s) in `_values` sent to the recipient.
		        //如果 `_ids` 中代币持有者的任何余额低于发送给接收者的 `_values` 中的相应金额，则必须恢复。
		        MUST revert on any other error. 
		        // 必须在任何其他错误时恢复。
		        MUST emit `TransferSingle` or `TransferBatch` event(s) such that all the balance changes are reflected (see "Safe Transfer Rules" section of the standard).
		        // 必须发出 `TransferSingle` 或 `TransferBatch` 事件，以便反映所有余额变化（参见标准的“Safe Transfer Rules”部分）
		        Balance changes and events MUST follow the ordering of the arrays (_ids[0]/_values[0] before _ids[1]/_values[1], etc).
		        // 余额变化和事件必须遵循数组的顺序（_ids[0]/_values[0] 在 _ids[1]/_values[1] 之前，等等）
		        After the above conditions for the transfer(s) in the batch are met, this function MUST check if `_to` is a smart contract (e.g. code size > 0). If so, it MUST call the relevant `ERC1155TokenReceiver` hook(s) on `_to` and act appropriately (see "Safe Transfer Rules" section of the standard). 
		        // 在批处理中满足上述转移条件后，此函数必须检查 “_to” 是否为智能合约（例如代码大小 > 0）。如果是它必须在 `_to` 上调用相关的 `ERC1155TokenReceiver` 钩子并采取适当的行动（参见标准的“安全传输规则”部分）
		         
		        @参数                     
			        - _from   _from 源地址
			        -  _to     _to 目标地址
			        - _ids     每种代币类型的 ID（顺序和长度必须与 _values 数组匹配）
			        - _values  每种代币类型的转账金额（顺序和长度必须匹配 _ids 数组）
			        -  _data   没有指定格式的附加数据，必须在调用 `_to` 上的 `ERC1155TokenReceiver` 钩子时不变地发送
		    */
		    
		    function safeBatchTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external;
		
		    /**
		        @notice Get the balance of an account's tokens. 
		        //获取账户代币的余额。
		        @return        The _owner's balance of the token type requested 
		        //_owner 的请求的代币类型的余额
		        
		        @参数
			        - _owner  代币持有者的地址
			        - _id   代币的 ID
		       
		     */
		     
		    function balanceOf(address _owner, uint256 _id) external view returns (uint256);
		
		    /**
		        @notice Get the balance of multiple account/token pairs 
		        //获取多个账户/代币对的余额
		        
		        @return        The _owner's balance of the token types requested (i.e. balance for each (owner, id) pair) 
		        //_owner 的请求的代币类型的余额(列表每个 owner,id 对应的余额)
		        
		        @参数
		       	-  _owners  代币持有者的地址
			      - _ids  代币的 ID
		     */
		     
		    function balanceOfBatch(address[] calldata _owners, uint256[] calldata _ids) external view returns (uint256[] memory);
		
		    /**
		        @notice Enable or disable approval for a third party ("operator") to manage all of the caller's tokens.
		        // 启用或禁用第三方（“operator”）管理所有调用者令牌的批准。
		        
		        @dev MUST emit the ApprovalForAll event on success.
		        // 必须在成功时发出 ApprovalForAll 事件。
		        
		        @参数
		        	-  _operator 添加到授权 operator 集合的地址
		        	- _approved 授权，如果 operator 被授权为 True，取消授权为 false
		    */
		    
		    function setApprovalForAll(address _operator, bool _approved) external;
		
		    /**
		        @notice Queries the approval status of an operator for a given owner.
		       // 查询给定所有者的操作员的批准状态。
		        
		        @return          如果 operator 被授权为 True，取消授权为 false
		        
		        @参数
		        	- _owner 代币的所有者
		        	- _operator 授权 operator 地址
		    */
		    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
		}
		
### ERC-1155 代币接收器合约
- 智能合约必须实现 `ERC1155TokenReceiver` 接口中的所有功能才能接受传输。有关详细信息，请参阅“Safe Transfer Rules”。
- 智能合约必须实现 ERC-165 `supportsInterface` 功能并表示支持 `ERC1155TokenReceiver` 接受传输的接口。有关更多详细信息，请参阅“ERC1155 TokenReceiver ERC-165 rules”。
- 标准代码

		pragma solidity ^0.5.9;
		
		/**
		    Note: The ERC-165 identifier for this interface is 0x4e2312e0.
		    注意：此接口的 ERC-165 标识符为 0x4e2312e0。
		*/
		
		interface ERC1155TokenReceiver {
		   
		    /**
		        @notice Handle the receipt of a single ERC1155 token type.
		        // 处理单个 ERC1155 令牌类型的接收。
		        @dev An ERC1155-compliant smart contract MUST call this function on the token recipient contract, at the end of a `safeTransferFrom` after the balance has been updated. 
		        // 符合 ERC1155 的智能合约必须在余额更新后的 “safeTransferFrom” 结束时在代币接收者合约上调用此函数
		        This function MUST return `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` (i.e. 0xf23a6e61) if it accepts the transfer.
		        // 如果该函数接受传输，则该函数必须返回 `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`  （例 0xf23a6e61）
		        This function MUST revert if it rejects the transfer.
		        // 如果它拒绝传输，这个函数必须恢复。
		        Return of any other value than the prescribed keccak256 generated value MUST result in the transaction being reverted by the caller.
		        // 返回除规定的 keccak256 生成值之外的任何其他值必须导致调用者恢复交易
		        
		        @return           `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`
		         
		        @参数
			        - _operator  发起传输的地址（即 msg.sender）
			        -  _from  之前拥有代币的地址
			        - _id  被转账的代币ID
			        - _value   被转移的代币数量
			        - _data  没有指定格式的附加数据
		       
		    */
		    
		    function onERC1155Received(address _operator, address _from, uint256 _id, uint256 _value, bytes calldata _data) external returns(bytes4);
		
		    /**
		        @notice Handle the receipt of multiple ERC1155 token types. 
			  //处理多种 ERC1155 代币类型的接收。
		        @dev An ERC1155-compliant smart contract MUST call this function on the token recipient contract, at the end of a `safeBatchTransferFrom` after the balances have been updated. 
		        // 符合 ERC1155 的智能合约必须在余额更新后的 “safeBatchTransferFrom” 结束时在代币接收者合约上调用此函数。       
		        This function MUST return `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` (i.e. 0xbc197c81) if it accepts the transfer(s).
		        //如果该函数接受传输，则该函数必须返回  `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` (例 0xbc197c81) 
		        This function MUST revert if it rejects the transfer(s).
		        如果此函数拒绝传输，则它必须恢复。
		        Return of any other value than the prescribed keccak256 generated value MUST result in the transaction being reverted by the caller.
		        返回除规定的 keccak256 生成值之外的任何其他值必须导致调用者恢复交易。
		        
		        @return           `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`
		        
		        @参数
			         - _operator  发起传输的地址（即 msg.sender）
			   	   -  _from  之前拥有代币的地址
		        	   - _ids   包含正在传输的每个令牌的 id 的数组（顺序和长度必须与 _values 数组匹配）
		        	   - _values    一个包含每个被传输代币数量的数组（顺序和长度必须与 _ids 数组匹配）
		        	   - _data   没有指定格式的附加数据
		      
		    */
		    function onERC1155BatchReceived(address _operator, address _from, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external returns(bytes4);       
		}

## 安全转移规则（Safe Transfer Rules）
为了更明确地说明标准 `safeTransferFrom` 和 `safeBatchTransferFrom` 函数必须如何与 `ERC1155TokenReceiver` 钩子函数相关联，下面列出了场景和规则。
### 场景
- 场景 #1：接收方不是合同。
	- `onERC1155Received` 和 `onERC1155BatchReceived` 必须不在(MUST NOT) EOA（外部拥有的账户）上调用。
- 场景 #2：交易不是代币的铸造/转移。
	- `onERC1155Received` 和 `onERC1155BatchReceived` 必须不在(MUST NOT) 铸造或转移过程之外调用。
- 场景 #3：接收方没有实现必要的 `ERC1155TokenReceiver` 接口功能。
	- 转移必须(MUST)通过下面的一个警告恢复。
		- 如果被发送的代币是另一个标准的混合实现的一部分，那么现在可以遵循该特定标准关于发送到合约的规则。请参阅“向后兼容性(Backwards Compatibility)”部分。
- 场景#4：接收器实现了必要的 `ERC1155TokenReceiver` 接口函数，但返回了一个未知值。
	- 转移必须(MUST)被恢复。
- 场景#5：接收器实现了必要的 `ERC1155TokenReceiver` 接口函数但抛出了错误。
	- 转移必须(MUST)被恢复。
- 场景#6：接收者实现 `ERC1155TokenReceiver` 接口并且是一次且仅一次余额变化的接收者（例如 `safeTransferFrom` 被调用）。
	- 在 `ERC1155TokenReceiver` 调用接收者合约的钩子之前，必须(MUST)更新转移的余额。
	- 在 `ERC1155TokenReceiver` 调用接收者合约的钩子之前，必须(MUST)已经发出 transfer 事件以反映余额变化。
	- 其中之一 `onERC1155Received` 或 `onERC1155BatchReceived` 必须在接收方合约上调用。
	- 该 `onERC1155Received` 钩子应该叫收件人合同及其规则遵循。
		- 有关必须(MUST)遵循的进一步规则，请参阅`onERC1155Received` 规则。
	- 该`onERC1155BatchReceived`挂钩可称为收件人合同及其规则遵循。
		- 有关必须(MUST)遵循的更多规则，请参阅 `onERC1155BatchReceived` 规则。
- 场景 #7：接收者实现 `ERC1155TokenReceiver` 接口并且是多个余额变化的接收者（例如 `safeBatchTransferFrom` 被调用）。
	- 在调用 `ERC1155TokenReceiver` 挂钩中引用的所有余额转移必须(MUST) `ERC1155TokenReceiver` 在接收者合约上调用挂钩之前更新。
	- `ERC1155TokenReceiver` 在对接收者合约调用钩子之前，必须(MUST)发出所有传输事件以反映当前余额的变化。
	- `onERC1155Received` 或 `onERC1155BatchReceived` 必须(MUST)根据需要多次调用接收方，以便计算场景中接收方的每次余额变化。
		- 必须(MUST)根据 `onERC1155Received rules` 和 `onERC1155BatchReceived rules` 检查并执行每个钩子调用的返回魔术值。
	- 该 `onERC1155BatchReceived` 钩子应该叫收件人合同及其规则遵循。
		- 有关必须遵循的更多规则，请参阅 `onERC1155BatchReceived rules`。
	- 该 `onERC1155Received` 挂钩可称为收件人合同及其规则遵循。
		- 有关必须遵循的进一步规则，请参阅 `onERC1155Received rules`。
- 场景 #8：你是一个合同的创作者实现了 `ERC1155TokenReceiver` 在一个界面，你转发的代币（S）到另一个地址或两者的`onERC1155Received` 和 `onERC1155BatchReceived`。
	- 转发应被视为接受，然后启动新的 `safeTransferFrom` 或 `safeBatchTransferFrom` 在新的上下文中。
		- 转发成功后，必须(MUST)返回被调用的接收器钩子的规定 keccak256 接受 value magic 。
	- 该 `_data` 参数可能(MAY)会被重新用于新的上下文。
	- 如果转发失败，交易可能会被恢复。
		- 如果合约逻辑希望在这种情况下保留代币本身的所有权，它可以这样做。
- 场景 #9：您通过非标准 API 调用（即特定于实现的 API 和 NOT `safeTransferFrom` 或 `safeBatchTransferFrom`.
	- 在这种情况下，所有余额更新和事件输出规则都与调用标准传递函数一样。
		- 即外部查看器必须(MUST)仍然能够通过标准函数查询余额，并且它必须(MUST)与单独由 `TransferSingle` 和 `TransferBatch` 事件确定的余额相同。
	- 如果接收者是一个合约，`ERC1155TokenReceiver` 钩子仍然需要被调用并且返回值与调用标准传递函数一样。
		- 然而，如果接收合约没有实现接口，则 `safeTransferFrom` 或 `safeBatchTransferFrom` 函数必须(MUST)恢复`ERC1155TokenReceiver`，非标准函数可以继续传输。
		- 请参阅“实现特定传输 API 规则”。

### 规则
- `safeTransferFrom rules`
	- 调用者必须获得 Approval 才能管理从 `_from` 账户中转出的代币（请参阅“Approval”部分）。
	- 如果 `_to` 是零地址，则必须(MUST)恢复。
	- 如果代币持有者的余额 `_id` 低于 `_value` 发送给接收者的余额，则必须(MUST)恢复。
	- 必须(MUST)在任何其他错误时恢复。
	- 必须(MUST)发出 `TransferSingle` 事件以反映余额变化（参见`TransferSingle 和 TransferBatch 事件规则`部分）。
	- 满足上述条件后，此函数必须(MUST)检查是否 `_to` 为智能合约（例如代码大小 > 0）。如果是这样，它必须调用 `onERC1155Received` 上的`_to`，并采取适当的行动（见 `onERC1155Received rules `一节）。
		- `_data` 发送方为传输提供的参数必须(MUST)连同其内容未经更改地 `onERC1155Received` 通过其 `_data` 参数传递给钩子函数。
- `safeBatchTransferFrom rules`
	- 调用者必须获得 Approval 才能管理从 `_from` 账户中转出的代币（请参阅“Approval”部分）。
	- 如果 `_to` 是零地址，则必须(MUST)恢复。
	- 如果长度 `of_ids` 与长度不同，则必须(MUST)恢复 `_values`。
	- 如果代币持有者的任何余额 `_ids` 低于 `_values` 发送给接收者的相应金额，则必须(MUST)还原。
	- 必须(MUST)在任何其他错误时恢复。
	- 必须(MUST)发出 `TransferSingle` 或 `TransferBatch` 事件，以便反映所有余额更改（请参阅`TransferSingle` 和 `TransferBatch` 事件规则部分）。
	- 余额变化和事件必须(MUST)按照它们提交的数组顺序发生（`_ids[0]/_values[0]` 在 `_ids[1]/_values[1] `之前，等等）。
	- 满足上述条件后，此函数必须检查是否 `_to` 为智能合约（例如代码大小 > 0）。如果是它必须调用 `onERC1155Received` 或`onERC1155BatchReceivedon` 并采取适当的行动（参见`onERC1155Received`和 `onERC1155BatchReceived` 规则部分）。
		- `_data`发送者为传输提供的参数必须 `ERC1155TokenReceiver` 通过它们的 `_data` 参数传递给钩子函数。
- `TransferSingle` 和 `TransferBatch `事件规则
	- `TransferSingle` 应该用于表示在一个 `_from` 和 `_topair` 之间发生了单笔余额转移。
		- 它可能会被多次发出以指示交易中的多次余额变化，但请注意这 `TransferBatch` 是为了减少 gas 消耗而设计的。
		- `_operator` 参数必须(MUST)是经批准进行转让账户/合同的地址（应该是 `msg.sender`）。
		- `_from` 参数必须(MUST)是其资产负债减少持有人的地址。
		- `_to` 参数必须(MUST)是其余额增加收件人的地址。
		- `_id` 参数必须(MUST)是正在传送的代币类型。
		- `_value` 参数必须(MUST)是代币持有者平衡被减少的数量和内容相匹配的收件人余额增加。
		- 铸造/创建代币时，`_from` 必须(MUST)将参数设置为 `0x0`（即零地址）。请参阅 `创建/创建和销毁/销毁规则`。
		- 销毁/销毁代币时，`_to` 必须(MUST)将参数设置为 `0x0`（即零地址）。请参阅 `创建/创建和销毁/销毁规则`。
	- `TransferBatch` 应该用于指示在一对 `_from` 和 `_to` 对之间发生了多次余额转移。
		- 它可以与列表中的单个元素一起发出，以指示交易中的单一余额变化，但请注意，这 `TransferSingle` 是为减少 gas 消耗而设计的。
		- `_operator` 参数必须(MUST)是经批准进行转让账户/合同的地址（应该是 `msg.sender`）。
		- `_from` 参数必须(MUST)是其平衡降低在每个条目对中的保持器的地址 `_ids` 和 `_values`。
		- `_to`参数必须(MUST)是其余额在每个条目增加对收件人的地址 `_ids` 和 `_values`。
		- `_ids`数组参数必须包含被传送的代币的标识。
		- `_values` 数组参数必须(MUST)包含要传送用于在每个相应条目的令牌的数量 `_ids`。
		- `_ids` 并且 `_values` 必须具有相同的长度。
		- 铸造/创建代币时，`_from` 必须(MUST)将参数设置为 `0x0`（例零地址）。请参阅 `创建/创建和销毁/销毁规则`。
		- 销毁/销毁代币时，`_to` 必须(MUST)将参数设置为 `0x0`（例零地址）。请参阅 `创建/创建和销毁/销毁规则`。
	- 从地址转移的总价值 `0x0` 减去 `0x0` 通过 `TransferSingle` 和 `TransferBatch` 事件转移到观察到的总价值，可以被客户和交易所用来确定给定代币 ID 的“循环供应”。
	- 要广播没有初始余额的代币 ID 的存在，合约 `TransferSingle` 应该从 `0x0` 发出事件到 `0x0`，代币创建者为 `_operator`，`_value` 为 0。
	- 必须(MUST)发出 `AllTransferSingle` 和 `TransferBatch` 事件以反映在对 `onERC1155Receivedor` 的任何调用之前发生的所有余额变化`onERC1155BatchReceived`。
		- 为了确保在有效重新输入的情况下事件顺序是正确的（例如，如果接收者合约在接收时转发代币）状态余额和事件余额必须在调用外部合约之前匹配。 
- `onERC1155Received` rules
	- `_operator` 参数必须(MUST)是经 `approved` 进行转让账户/合同的地址（应该是 `msg.sender`）
	- `_from` 参数必须(MUST)是其资产负债减少持有人的地址。
		- `_from` 铸造必须是 0x0。
	- `_id` 参数必须(MUST)是正在传送的代币类型。
	- `_value` 参数必须(MUST)是代币持有者余额被减少的数量和内容相匹配的收件人余额增加。
	- `_data` 参数必须(MUST)包含由发送方用于与它的内容未改变的传送提供的信息。
		- 即它必须传递 `_data` 通过 `safeTransferFrom` 或者 `safeBatchTransferFrom` 调用发送的未更改参数以进行此传输。
	- 接收者合约可以通过返回接受魔法值来接受其余额的增加 `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`
		- 如果返回值是 `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` 传输必须完成或必须(MUST) 在任何其他条件不满足时恢复成功。	
	- 接收者合约可以通过调用 revert 来拒绝增加其余额
		- 如果接收方合约抛出/恢复交易，则必须恢复交易。
	- 如果返回值不是 `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` 交易，则必须恢复。
	- `onERC1155Received`（和/或 `onERC1155BatchReceived`）可以在单个事务中多次调用，并且必须(MUST)满足以下要求：
		- 所有回调都代表互斥的余额变化。
		- 对所有调用的集合，`onERC1155Received` 和 `onERC1155BatchReceived` 描述了在提交的订单中的交易期间发生的所有余额变化。
	- `onERC1155Received` 如果转移操作正在将代币转移到自身，则合约可以跳过调用钩子函数。
- `onERC1155BatchReceived rules `
	- `_operator` 参数必须(MUST)是经 `approved` 进行转让账户/合同的地址（应该是 `msg.sender`）
	- `_from` 参数必须是其资产负债减少持有人的地址。
		- ` _from` 铸币必须是 0x0。
	- `_ids` 参数必须是被转移的代币的列表。
	- `_values` 参数必须是代币数量（匹配中指定的代币的列表，并顺序的号码的 `_ids` 列表 ）的保持器的余额是通过减少与内容相匹配的收件人余额增加。
	- `_data` 参数必须包含由发送方用于与它的内容未改变的传送提供的信息。
		- 即它必须传递 `_data` 通过 `safeBatchTransferFrom` 调用此传输发送的未更改参数。
	- 接收者合约可以通过返回接受魔法值来接受其余额的增加 `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`
		- 如果返回值是 `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` 传输必须完成或必须在任何其他条件不满足时恢复成功。
	- 接收者合约可以通过调用 `revert` 来拒绝增加其余额。
		- 如果接收方合约抛出/恢复交易，则必须恢复交易。
	- 如果返回值不是 `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` 交易，则必须恢复。
	- `onERC1155BatchReceived`（和/或onERC1155Received）可以在单个事务中多次调用，并且必须满足以下要求：
		- 所有回调都代表互斥的余额变化。
		- 对所有调用的集合，`onERC1155Received` 和 `onERC1155BatchReceived` 描述了在提交的订单中的交易期间发生的所有余额变化。
	- `onERC1155BatchReceived` 如果转移操作正在将代币转移给自身，则合约可以跳过调用钩子函数。
- `ERC1155TokenReceiver ERC-165 rules`
	- `ERC-165supportsInterface` 函数的实现应该如下
	
			  function supportsInterface(bytes4 interfaceID) external view returns (bool) {
			      return  interfaceID == 0x01ffc9a7 ||    // ERC-165 支持的 (例如 `bytes4(keccak256('supportsInterface(bytes4)'))`).
			              interfaceID == 0x4e2312e0;      // ERC-1155 `ERC1155TokenReceiver` 支持的 (i.e. `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)")) ^ bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`).
			  }
	- 实现可能与上述不同，但：
		- `true` 如果 `0x01ffc9a7` 通过 `interfaceID` 参数传递，它必须返回常量值。这表示支持 `ERC-165`。
		- `true` 如果 `0x4e2312e0` 通过 `interfaceID` 参数传递，它必须返回常量值。这表示 `ERC1155TokenReceiver` 支持 ERC-1155 。
		- 它不得消耗超过 10,000 的气体。
			- 这使其低于 ERC-165 对 30,000 气体的要求，减少了气体储备需求并最大限度地减少了通话期间气体耗尽的可能副作用。
- `Implementation specific transfer API rules` //实现特定的传输 API 规则
	- 如果使用特定于实现的 API 函数将 ERC-1155 代币传输到合约，则在接收方实现接口的情况下仍必须遵循 `safeTransferFrom` 或 `safeBatchTransferFrom`（视情况而定）规则 `ERC1155TokenReceiver`。
	- 如果不是，非标准实现应该恢复但可以继续。

	一个例子

	- 经 `approved` 的用户调用诸如 `function myTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _values);。` 的函数
	- `myTransferFrom` 更新所有`_ids_`和`values`的余额的 `_from` 和 `_to` 地址。
	- `myTransferFrom` 发出 `TransferBatch` 从 address `_from` 到 address `_to` 传输的内容的详细信息。
	- `myTransferFrom` 检查 `_to` 是否是合约地址并确定是（如果不是，则可以认为转移成功）。
	- `myTransferFrom` 调用  `onERC1155BatchReceived` 上的 `_to` 并将其恢复或返回未知值（如果它返回`bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))` 的转移可以被认为是成功的）。
	- 此时 `myTransferFrom` 应该立即恢复交易，因为 `onERC1155BatchReceived` 函数没有明确接受代币的接收。
	- 然而如果 `myTransferFrom` 希望继续它必须调用 `_to` 的 `supportsInterface(0x4e2312e0)` ，如果它返回的恒定值 `true` 的事务必须被还原，因为它是目前已知是有效的接收器和失败以前接受步骤。
		- 注意：`supportsInterface(0x4e2312e0)` 如果您想更早地收集该信息并对其采取行动，例如在混合标准方案中，您可以在上一步调用。
	- 如果上面对 `supportsInterface(0x4e2312e0)`  在调用 `_to` 恢复或返回一个非常量值的值 `true`，`myTransferFrom` 函数可以认为这次传输成功。
		- 注意：如果发送到不希望收到 ERC-1155 代币的地址，这可能会导致不可恢复的代币。
	
	上面的例子并不详尽，但说明了主要观点（并表明大多数与`safeTransferFrom` 和共享 `safeBatchTransferFrom）
	
	- 更新的余额必须发出等效的转移事件。
	- 必须检查接收者地址是否是合约，如果是，则 `ERC1155TokenReceiver` 必须在其上调用相关的钩子函数。
	- 调用 `ERC1155TokenReceiver` 钩子时引用的余额（和相关事件）必须在调用钩子之前更新（和发出）`ERC1155TokenReceiver`。
	- `ERC1155TokenReceiver` 如果实现了被调用的钩子函数的返回值，则必须遵守它们。
	- 只有非标准的传输函数可以允许将代币发送到没有实现必要 `ERC1155TokenReceiver` 挂钩函数的接收者合约。`safeTransferFrom` 并且 `safeBatchTransferFrom` 必须在这种情况下恢复（除非它是混合标准实现，请参阅“向后兼容性”）。
- `Minting/creating and burning/destroying rules`
	- `mint/create` 操作本质上是一种专门的传输，必须遵循以下规则：
		- 要广播没有初始余额的代币 ID 的存在，合约应该 `TransferSingle` 从 `0x0` `to` 发出事件 `0x0`，令牌创建者为 `_operator`，`_value`为 0。
		- `TransferSingle` 和 `TransferBatch` 事件规则必须适用于铸币厂（即单件或批次），但 `_from` 必须将参数设置为 `0x0`（即零地址）以向合约观察者标记转移为铸币厂。
			- 注意：这包括在合约中获得初始余额的代币。合约的余额也必须能够单独由事件决定，这意味着初始合约余额（例如在建设中）也必须发出事件来反映这些余额。
	- `burn/destroy` 操作本质上是一种专门的传输，必须遵循以下规则：
		- `TransferSingle` 和 `TransferBatch` 事件规则必须适用于销毁（即单件或批次），但 `_to` 必须将参数设置为 `0x0`（即零地址）以向合约观察者标记转移为销毁。
		- `burn/destroy`时您不必实际转移到 `0x0`（即具体实施），只有 `_to` 事件中的参数必须设置 `0x0` ,如上。
	- 从地址转移的总价值 `0x0` 减去 `0x0` 通过 `TransferSingle` 和 `TransferBatch` 事件转移到观察到的总价值，可以被客户和交易所用来确定给定代币 ID 的“循环供应”。
	- 如上所述，`mint/create` 和 `burn/destroy` 操作是专门的传输，因此很可能使用自定义传输函数而不是 `safeTransferFrom` 或来完成`safeBatchTransferFrom` 。如果是这样，“实现特定的传输 API 规则”部分将是合适的。
		- 即使在不安全的 API 和/或混合标准的情况下，在`mint/create` 和 `burn/destroy`时仍必须遵守上述事件规则。
	- `ERC1155TokenReceiver` 如果铸币操作正在将代币转移给自身，则合约可以跳过调用钩子函数。在所有其他情况下，`ERC1155TokenReceiver` 必须遵循适用于实施的规则（即安全、自定义和/或混合）。

keccak256 为各种魔法值生成常量的 solidity 示例（这些可能被实现使用）

	bytes4 constant public ERC1155_ERC165 = 0xd9b67a26; // ERC-165 identifier for the main token standard.
	bytes4 constant public ERC1155_ERC165_TOKENRECEIVER = 0x4e2312e0; // ERC-165 identifier for the `ERC1155TokenReceiver` support (i.e. `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)")) ^ bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`).
	bytes4 constant public ERC1155_ACCEPTED = 0xf23a6e61; // Return value from `onERC1155Received` call if a contract accepts receipt (i.e `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`).
	bytes4 constant public ERC1155_BATCH_ACCEPTED = 0xbc197c81; // Return value from `onERC1155BatchReceived` call if a contract accepts receipt (i.e `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`).

### 元数据
URI 值允许客户端替换 ID。如果字符串 `{id}` 存在于任何 URI 中，客户端必须用十六进制形式的实际代币 ID 替换它。通过为大量代币定义一次 URI，这允许大量代币使用相同的链上字符串。

- 被替换的十六进制 ID 的字符串格式必须是小写字母数字：`[0-9a-f]` 没有 `0x` 前缀。
- 如有必要,替换的十六进制 ID 的字符串格式必须前导零填充到 64 个十六进制字符长度。

URI的示例：

	https://token-cdn-domain/{id}.json
将被替换
	
	https://token-cdn-domain/000000000000000000000000000000000000000000000000000000000004cce0.json
如果客户端指的代币 ID 314592 / 0x4CCE0。

#### 元数据扩展
可选 `ERC1155Metadata_URI` 扩展可以通过 [ERC-165 标准接口检测来识别](https://eips.ethereum.org/EIPS/eip-165)。

如果包含可选 `ERC1155Metadata_URI` 扩展名

- 如果通过参数传递，ERC-165 `supportsInterface` 函数必须返回常量值。如 `true 0x0e89341c interfaceID`
- `URI` 如果更改可以用事件表示（即它不是动态/程序化的），则对 URI 的更改必须发出该事件。
	- 一个实现可以 `URI` 在 `mint` 操作期间发出事件，但这不是强制性的。`uri` 如果未发出，观察者可以在创建时从函数中获取元数据 `uri` 。
- `uri` 如果未发出事件，则该函数应该用于检索值
- `uri` 函数必须返回与 `_id` 发出的最新事件相同的值
- `uri` 函数不得用于检查代币是否存在，因为即使代币不存在，实现也可能返回有效字符串。

ERC1155Metadata_URI 合约

	pragma solidity ^0.5.9;
	
	/**
	    Note: The ERC-165 identifier for this interface is 0x0e89341c.
	    // 注意：此接口的 ERC-165 标识符为 0x0e89341c。
	*/
	interface ERC1155Metadata_URI {
	    /**
	        @notice A distinct Uniform Resource Identifier (URI) for a given token.
	        // 给定代币的独特统一资源标识符 (URI)。
	        @dev URIs are defined in RFC 3986.
	        // URI 在 RFC 3986 中定义。
	        The URI MUST point to a JSON file that conforms to the "ERC-1155 Metadata URI JSON Schema". 
	        // URI 必须 MUST 指向符合“ERC-1155 元数据 URI JSON 模式”的 JSON 文件。       
	        @return URI string
	    */
	    function uri(uint256 _id) external view returns (string memory);
	}
	
#### ERC1155 元数据
此 JSON 模式松散地基于 `ERC721 元数据 JSON 模式`，但包括可选格式以允许客户端替换 ID。如果字符串 `{id}` 存在于任何 JSON 值中，则必须由所有遵循此标准的客户端软件替换为实际的代币 ID。

- 被替换的十六进制 ID 的字符串格式必须是小写字母数字：`[0-9a-f]` 没有 `0x` 前缀。
- 如有必要，替换的十六进制 ID 的字符串格式必须前导零填充到 64 个十六进制字符长度。

##### 规范
	{
	    "title": "Token Metadata",
	    "type": "object",
	    "properties": {
	        "name": {
	            "type": "string",
	            "description": "标识此代币所代表的资产"
	        },
	        "decimals": {
	            "type": "integer",
	            "description": "代币金额应显示的小数位数 - 例如 18、表示将代币数量除以 1000000000000000000 得到其用户代表"
	        },
	        "description": {
	            "type": "string",
	            "description": "描述此代币所代表的资产"
	        },
	        "image": {
	            "type": "string",
	            "description": "指向具有 MIME 类型 image/* 的资源的 URI，代表此代币所代表的资产。 考虑制作宽度在 320 到 1080 像素之间且纵横比在 1.91:1 到 4:5 之间（含）的任何图像。"
	        },
	        "properties": {
	            "type": "object",
	            "description": "任意属性。 值可以是字符串、数字、对象或数组"
	        }
	    }
	}
下面是 ERC-1155 元数据 JSON 文件的示例。properties 数组为特定于代币的显示属性和元数据提出了一些 SUGGESTED 格式。

	{
		"name": "Asset Name",
		"description": "Lorem ipsum...",
		"image": "https:\/\/s3.amazonaws.com\/your-bucket\/images\/{id}.png",
		"properties": {
			"simple_property": "example value",
			"rich_property": {
				"name": "Name",
				"value": "123",
				"display_value": "123 Example Value",
				"class": "emphasis",
				"css": {
					"color": "#ffffff",
					"font-weight": "bold",
					"text-decoration": "underline"
				}
			},
			"array_property": {
				"name": "Name",
				"value": [1,2,3,4],
				"class": "emphasis"
			}
		}
	}
##### 内容本地化
元数据本地化应该标准化，以提高所有语言的表示统一性。因此，提出了一种简单的叠加方法来实现定位。如果元数据 JSON 文件包含一个`localization` 属性，则其内容可以用于为需要它的字段提供本地化值。该 `localization` 属性应该是具有三个属性的子对象：`uri、default` 和`locales` 。如果该字符串 `{locale}` 存在于任何 URI 中，则所有客户端软件都必须将其替换为所选的语言环境

- JSON 架构
	
		{
			    "title":"Token Metadata",
			    "type":"object",
			    "properties":{
			        "name":{
			            "type":"string",
			            "description":"标识此代币所代表的资产"
			        },
			        "decimals":{
			            "type":"integer",
			            "description":"代币金额应显示的小数位数 - 例如 18、表示将代币数量除以 1000000000000000000 得到其用户代表"
			        },
			        "description":{
			            "type":"string",
			            "description":"描述此代币所代表的资产"
			        },
			        "image":{
			            "type":"string",
			            "description":"指向具有 MIME 类型 image/* 的资源的 URI，代表此代币所代表的资产。 考虑制作宽度在 320 到 1080 像素之间且纵横比在 1.91:1 到 4:5 之间（含）的任何图像."
			        },
			        "properties":{
			            "type":"object",
			            "description":"任意属性。 值可以是字符串、数字、对象或数组"
			        },
			        "localization":{
			            "type":"object",
			            "required":[
			                "uri",
			                "default",
			                "locales"
			            ],
			            "properties":{
			                "uri":{
			                    "type":"string",
			                    "description":"从中获取本地化数据的 URI 模式。 此 URI 应包含子字符串“{locale}”，它将在发送请求之前替换为适当的区域设置值."
			                },
			                "default":{
			                    "type":"string",
			                    "description":"基本 JSON 中默认数据的语言环境"
			                },
			                "locales":{
			                    "type":"array",
			                    "description":"数据可用的区域设置列表。 这些语言环境应符合 Unicode 通用语言环境数据存储库 (http://cldr.unicode.org/)."
			                }
			            }
			        }
			    }
			}
- 本地化样例
	- Base URI

			{
			    "name":"Advertising Space",
			    "description":"Each token represents a unique Ad space in the city.",
			    "localization":{
			        "uri":"ipfs://QmWS1VAdMD353A6SDk9wNyvkT14kyCiZrNDYAad4w1tKqT/{locale}.json",
			        "default":"en",
			        "locales":[
			            "en",
			            "es",
			            "fr"
			        ]
			    }
			}
	- `es.json`

			{
			  "name": "Espacio Publicitario",
			  "description": "Cada token representa un espacio publicitario único en la ciudad."
			}
	- `fr.json`

			{
			  "name": "Espace Publicitaire",
			  "description": "Chaque jeton représente un espace publicitaire unique dans la ville."
			}
			
#### Approval
该功能 `setApprovalForAll` 允许 operator 代表 approver 管理一个人的整个代币集。为了允许 approval 代币 ID 的子集，建议使用诸如 [`ERC-1761` 范围 apprpval 接口](https://eips.ethereum.org/EIPS/eip-1761)之类的接口。对应方 `isApprovedForAll` 提供对由设置的任何状态的内省 `setApprovalForAll`。

无论 approval 状态如何，所有者都应该被假定始终能够对自己的代币进行操作，所以不应该必须调用`setApprovalForAll` 来 approval 拥有者作为一个operator ，然后才可以操作它们。
## 基本原理
### 元数据选择
- `symbol` 功能

	（在 ERC-20 和 ERC-721 标准中发现）未包括在内，因为我们认为这不是用于识别通用虚拟物品/资产的全局有用数据，并且也容易发生冲突。简写符号用于股票代码和货币交易，但在该领域之外它们并不那么有用。
- `name` 功能

	（对于人类可读的资产名称，链上）已从标准中删除，以允许元数据 JSON 成为最终资产名称并减少数据重复。这也允许名称的本地化，否则如果每个语言字符串都存储在链上，这将非常昂贵，更不用说膨胀标准接口了。虽然这一决定可能会给实施者增加一个小的负担来托管包含元数据的 JSON 文件，但我们相信 ERC-1155 的任何认真实施都将使用 JSON 元数据。

###  升级
发出 `TransferSingle` 或 `TransferBatch` 余额更改的要求意味着重新部署到新合约地址的 ERC-1155 的有效实现必须从新合约地址发出事件以复制已弃用的合约最终状态。仅发出最少数量的事件以仅反映最终余额并省略导致该状态的所有交易是有效的。事件发布要求是确保合约的当前状态始终只能通过事件进行跟踪。为了减少更改合约地址时发出事件的需要，请考虑使用代理模式，如 [EIP-2535](https://eips.ethereum.org/EIPS/eip-2535) 中所述。这还将具有为用户提供稳定合约地址的额外好处。

### 设计决策：支持非批量
该标准支持 `safeTransferFrom` 和 `onERC1155Received` 运行，因为它们对于单一令牌类型的传输来说要便宜得多，这可以说是一个常见的用例。
### 设计决策：仅安全传输
该标准仅支持安全风格的传输，使得接收方合约依赖 `onERC1155Received` 或 `onERC1155BatchReceived` 在传输结束时始终调用函数成为可能。
### 保证日志跟踪
随着以太坊生态系统的不断发展，许多 dapp 都依赖传统的数据库和资源管理器 API 服务来检索和分类数据。ERC-1155 标准保证智能合约发出的事件日志将提供足够的数据来创建所有当前代币余额的准确记录。数据库或浏览器可以侦听事件，并能够对合约中的每个 ERC-1155 代币提供索引和分类搜索。
### Approval
该功能 `setApprovalForAll` 允许 operator 代表拥有者管理一个人的整个令牌集。它实现了与交​​换和贸易合约的无摩擦交互。

可以通过附加接口或外部合同来限制对特定代币 ID、数量或其他规则的 approval 。其基本原理是使 ERC-1155 标准尽可能适用于所有用例，而不对可能不需要它的实现强加特定的 approval 方案。可以使用标准令牌批准接口，例如与 ERC-1155 兼容的建议的 [ERC-1761](https://eips.ethereum.org/EIPS/eip-1761) 范围批准接口。
### 向后兼容
在设计讨论期间，要求该标准在发送到合约地址时与现有标准兼容，特别是撰写本文时的 ERC-721。为了适应这种情况，如果合约没有实现上面的 `ERC1155TokenReceiver` “安全传输规则”部分，特别是“场景#3：接收方没有实现必要的`ERC1155TokenReceiver` 接口功能” ，则恢复逻辑有一些余地。

因此，在混合 ERC-1155 合约实现中，必须在接收方合约上进行额外的调用，并在对 `onERC1155Received` 或`onERC1155BatchReceived` 进行任何挂钩调用之前进行检查。因此，操作顺序必须是：

1. 实现必须调用 `supportsInterface(0x4e2312e0)` 接收者合约上的函数，提供至少 10,000 gas。
2. 如果函数调用成功并且返回值是常量值，`true` 则实现将作为常规 ERC-1155 实现进行，并调用 `onERC1155Received` 或`onERC1155BatchReceived` 挂钩和相关规则。
3. 如果函数调用失败或返回值不是常量值，`true` 则实现可以假设接收者合约不是，`ERC1155TokenReceiver`并遵循其其他标准的传输规则。

请注意，建议使用单一标准的纯实现而不是混合解决方案，但在实现下的参考部分中链接了混合 `ERC-1155/ERC-721` 合同的示例。

一个重要的考虑因素是，即使代币是用另一个标准的规则发送的，ERC-1155 传输事件仍然必须被发出。因此，根据 ERC-1155 标准规则，仍然可以仅通过事件来确定余额。

### 用法
该标准可用于表示整个域的多种代币类型。可替换和不可替换的代币都可以存储在同一个智能合约中。
#### 批量传输
`safeBatchTransferFrom` 功能允许批量传输多个 token ID 和值。ERC-1155 的设计使得批量传输成为可能，而无需像现有的代币标准那样需要包装器合约。与具有多笔交易的单笔转账相比，当批量转账中包含一种以上的代币类型时，这会降低 gas 成本。

标准化批量传输的另一个优点是智能合约能够在单个操作中使用 `onERC1155BatchReceived`.

建议客户端和钱包在发布批量转账时对代币 ID 和相关值（按升序）进行排序，因为在对 ID 进行排序时，某些 ERC-1155 实现可显着节省 gas 成本。有关此示例，请参阅 Horizo​​n Games - [Multi-Token Standard “packed balance” implementation](https://github.com/horizon-games/multi-token-standard)。
####  批次余额
`balanceOfBatch` 功能允许客户通过一次调用检索多个所有者和令牌 ID 的余额。
#### 从事件中枚举
为了使实施 ERC-1155 的合约的存储需求保持轻量，必须使用事件日志进行枚举（发现代币的 ID 和值）。建议交易所和区块链浏览器等客户端维护一个至少包含代币 ID、供应和 URI 的本地数据库。这可以从每个 `TransferSingle`、`TransferBatch` 和 URI 事件构建，从部署智能合约的区块开始，直到最新的区块。

因此，在创建、铸造、转移或销毁代币的任何情况下，ERC-1155 合约必须小心地发出 `TransferSingle` 或 `TransferBatch` 发生事件。
####  不可替换的代币
以下策略是您可以如何在同一合约中混合可替换和不可替换代币的示例。该标准不强制实施必须如何做到这一点。

- 拆分 ID 位

	`_id` 任何 ERC-1155 函数中 uint256 参数的前 128 位可能代表基本代币 ID，而后 128 位可能代表不可替换的索引以使其唯一。

	不可替换的代币可以使用基于索引的访问器与合约/代币数据集进行交互。因此，要访问混合数据合约中的特定代币集和该集合中的特定不可替换代币，`_id` 可以是 
	
		<uint128: base token id><uint128: index of non-fungible>.
要将不可替换的集合/类别识别为一个整体（或可替换的），您可以通过 `_id` 参数 as 传入基本 ID `<uint128: base token id><uint128: zero>`。如果您的实现使用这种技术，这自然意味着不可替换的索引应该是基于 1 的。

	在合约代码中，访问个体不可替换的两段数据可以用 uint128(~0) 和相同的掩码 128 来提取。

		uint256 baseTokenNFT = 12345 << 128;
		uint128 indexNFT = 50;
		
		uint256 baseTokenFT = 54321 << 128;
		
		balanceOf(baseTokenNFT, msg.sender); // 获取不可替换集 12345 的基本代币的余额(如果实现希望方便的话，可以使用此代币集获取用户的所有代笔的余额)。
		balanceOf(baseTokenNFT + indexNFT, msg.sender); // 在索引 50 处获取不可替换集合 12345 的代币余额(如果用户拥有单个的非可替代代币，则为1，否则为0)。
		balanceOf(baseTokenFT, msg.sender); // 获取可替换代币 54321 的余额

请注意，128 是一个任意数字，实现可以选择他们希望这种拆分如何发生以适合他们的用例。合约的观察者只会看到显示余额转移和铸币发生的事件，并且可以单独使用该信息跟踪余额。对于能够仅从 ID 确定类型（不可替代或可替代）的观察者，他们必须在逐个实现的基础上了解拆分 ID 位格式。

[ERC-1155 参考实现](https://github.com/enjin/erc-1155)是分割 ID 比特的策略的一个实例。

- 天然不可替代的代币

	表示不可替代的另一种简单方法是允许每个不可替代令牌的最大值为 1。这自然会反映现实世界，其中独特物品的数量为 1，而可替代物品的数量大于 1。

##  参考
- 标准
	- [ERC-721 不可替代代币标准](https://eips.ethereum.org/EIPS/eip-721)
	- [ERC-165 标准接口检测](https://eips.ethereum.org/EIPS/eip-165)
	- [ERC-1538 透明合约标准](https://eips.ethereum.org/EIPS/eip-1538)
	- [JSON 架构](https://json-schema.org/)
	- [RFC 2119 用于在 RFC 中表示需求级别的关键词](https://www.ietf.org/rfc/rfc2119.txt)
- 实现
	- [ERC-1155 参考实现-https://github.com/enjin/erc-1155](https://github.com/enjin/erc-1155)
	- [地平线游戏 - 多代币标准](https://github.com/horizon-games/multi-token-standard)
	- [恩金币](https://enjincoin.io/)（[GitHub](https://github.com/enjin)）
	- [沙盒 - 双 ERC-1155/721 合约](https://github.com/pixowl/thesandbox-contracts/tree/master/src/Asset)
- 文章和讨论
	- [GitHub - 原始讨论主题](https://github.com/ethereum/EIPs/issues/1155)
	- [ERC-1155 - 加密项目标准](https://blog.enjincoin.io/erc-1155-the-crypto-item-standard-ac9cf1c5a226)
	- [Here Be Dragons - 超越 ERC-20 和 ERC-721 将天然气成本降低约 80%](https://medium.com/horizongames/going-beyond-erc20-and-erc721-9acebd4ff6ef)
	- [Blockonomi - 以太坊 ERC-1155 令牌非常适合在线游戏，可能更多](https://blockonomi.com/erc1155-gaming-token/)
	- [超越游戏 - 探索 ERC-1155 代币标准的效用！](https://blockgeeks.com/erc-1155-token/)
	- [ERC-1155：沙盒的新标准](https://medium.com/sandbox-game/erc-1155-a-new-standard-for-the-sandbox-c95ee1e45072)			
					