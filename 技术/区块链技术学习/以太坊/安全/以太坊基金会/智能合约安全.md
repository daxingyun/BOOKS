# 智能合约安全
Solidity 于 2014 年 10 月启动，当时以太坊网络和虚拟机都没有进行任何实际测试，当时的 gas 成本甚至与现在大不相同。此外，一些早期的设计决策是从 Serpent 手中接手的。在过去的几个月里，最初被认为是最佳实践的示例和模式被暴露在现实中，其中一些实际上被证明是反模式。因此，我们最近更新了一些 [Solidity 文档](https://solidity.readthedocs.org/)，但由于大多数人可能不遵循 github 提交到该存储库的流，我想在这里强调一些发现。请在[文档](http://solidity.readthedocs.io/en/latest/miscellaneous.html#pitfalls)中阅读它们。
## 发送以太币
发送 Ether 应该是 Solidity 中最简单的事情之一，但事实证明它有一些大多数人没有意识到的微妙之处。

重要的是，充其量是由以太币的接收者发起支付。以下是拍卖合同的一个 BAD 示例：

	// 这是一个负面的例子！ 不要使用！
	contract auction {
	  address highestBidder;
	  uint highestBid;
	  function bid() {
	    if (msg.value < highestBid) throw;
	    if (highestBidder != 0)
	      highestBidder.send(highestBid); // refund previous bidder
	    highestBidder = msg.sender;
	    highestBid = msg.value;
	  }
	}
由于最大堆栈深度为 1024，新投标人始终可以将堆栈大小增加到 1023，然后调用 `bid()` 这将导致 `send(highestBid)` 调用静默失败（即前一个投标人不会收到退款），但新投标人仍将是最高的投标人。检查是否 `send` 成功的一种方法是检查其返回值

	/// 这仍然是一个负面的例子！ 不要使用！
	if (highestBidder != 0)
	  if (!highestBidder.send(highestBid))
	    throw;
该 `throw` 语句导致当前调用被还原。这是一个坏主意，因为接收方，例如通过实施后备功能，`function() { throw; }` 总是会迫使以太传输失败，这将产生没有人可以对她出价过高的效果。

防止这两种情况的唯一方法是通过让接收者控制传输来将发送模式转换为撤回模式：

	/// 这仍然是一个负面的例子！ 不要使用！ 
	contract auction {
	  address highestBidder;
	  uint highestBid;
	  mapping(address => uint) refunds;
	  function bid() {
	    if (msg.value < highestBid) throw;
	    if (highestBidder != 0)
	      refunds[highestBidder] += highestBid;
	    highestBidder = msg.sender;
	    highestBid = msg.value;
	  }
	  function withdrawRefund() {
	    if (msg.sender.send(refunds[msg.sender]))
	      refunds[msg.sender] = 0;
	  }
	}
为什么合同上面还要写“反例”？因为气体力学，合约其实还可以，但依然不是很好的例子。原因是，作为发送的一部分，不可能阻止接收方的代码执行。这意味着当发送函数仍在进行时，接收者可以回调 `withdrawRefund`。那时，退款金额仍然相同，因此他们将再次获得该金额，依此类推。在这个特定的例子中，它不起作用，因为接收者只获得了 gas 津贴（2100 gas），并且不可能用这个量的 gas 执行另一次发送。但是，以下代码容易受到这种攻击：

	msg.sender.call.value(refunds[msg.sender])().
考虑到这一切，下面的代码应该没问题（当然它仍然不是拍卖合约的完整示例）：

	contract auction {
	  address highestBidder;
	  uint highestBid;
	  mapping(address => uint) refunds;
	  function bid() {
	    if (msg.value < highestBid) throw;
	    if (highestBidder != 0)
	      refunds[highestBidder] += highestBid;
	    highestBidder = msg.sender;
	    highestBid = msg.value;
	  }
	  function withdrawRefund() {
	    uint refund = refunds[msg.sender];
	    refunds[msg.sender] = 0;
	    if (!msg.sender.send(refund))
	     refunds[msg.sender] = refund;
	  }
	}
请注意，我们没有在发送失败时使用 `throw`，因为我们能够手动恢复所有状态更改，并且不使用 `throw` 的副作用要少得多。
## 使用投标(throw)
`throw` 语句通常非常方便地恢复对状态所做的任何更改作为调用的一部分（或整个事务，取决于函数的调用方式）。但是，您必须注意，它还会导致所有 gas 被消耗，因此成本高昂，并且可能会停止对当前函数的调用。因此，我建议仅在以下情况下使用它：

1. 将以太币转账恢复为当前功能

	如果一个函数不打算接收以太币，或者不是在当前状态或当前参数，你应该使用 `throw` 来拒绝以太币。由于 gas 和堆栈深度问题，使用 `throw` 是可靠地发回 Ether 的唯一方法：接收方可能在后备函数中存在错误，该错误需要太多 gas，因此无法接收 Ether，或者该函数可能已被恶意调用堆栈深度太高的上下文（甚至可能在调用函数之前）。

	请注意，意外地将 Ether 发送到合约并不总是 UX 失败：您永远无法预测交易以何种顺序或何时添加到一个块中。如果合约被写成只接受第一笔交易，那么包含在其他交易中的以太币就必须被拒绝。
2. 还原被调用函数的效果

	如果你在其他合约上调用函数，你永远无法知道它们是如何实现的。这意味着这些调用的效果也是未知的，因此恢复这些效果的唯一方法是使用 `throw`。当然，如果你知道你将不得不恢复效果，你应该总是写你的合约，不要一开始就调用这些函数，但是有些用例你只有在事后才知道。

## 循环和阻塞 Gas 限制
一个区块可以消耗多少 gas 是有限制的。这个限制是灵活的，但很难增加它。这意味着在所有（合理的）情况下，合约中的每个函数都应保持在一定数量的 gas 以下。以下是投票合约的 BAD 示例：

	/// 这仍然是一个负面的例子！ 不使用！
	contract Voting {
	  mapping(address => uint) voteWeight;
	  address[] yesVotes;
	  uint requiredWeight;
	  address beneficiary;
	  uint amount;
	  function voteYes() { yesVotes.push(msg.sender); }
	  function tallyVotes() {
	    uint yesVotes;
	    for (uint i = 0; i < yesVotes.length; ++i)
	      yesVotes += voteWeight[yesVotes[i]];
	    if (yesVotes > requiredWeight)
	      beneficiary.send(amount);
	  }
	}
合约实际上有几个问题，但我想在这里强调的是循环的问题：

假设投票权重像代币一样是可转移和可拆分的（以 DAO 代币为例）。这意味着您可以创建任意数量的自己的克隆。创建这样的克隆会增加 `tallyVotes` 函数中循环的长度，直到它消耗的 gas 比单个块内的可用气体多。

这适用于任何使用循环的东西，也适用于循环在合约中不明确可见的地方，例如当您在存储中复制数组或字符串时。同样，如果循环的长度由调用者控制，则可以使用任意长度的循环，例如，如果您遍历作为函数参数传递的数组。但永远不要造成循环长度由一方控制的情况，该一方不会是唯一遭受失败的一方。

附带说明一下，这就是我们现在在 DAO 合约中包含冻结账户概念的原因之一：

	投票权重在投票时计算，以防止循环卡住的事实，如果投票直到投票期结束时权重才会固定，您可以通过转移您的代币然后再次投票来进行第二次投票。
## 接收以太币/回退功能
如果你想让你的合约通过常规的 `send()`  调用接收以太币，你必须让它的后备函数变得便宜。它只能使用 2300 gas，既不允许任何存储写入，也不允许沿 Ether 发送的函数调用。基本上，您在回退函数中唯一应该做的就是记录一个事件，以便外部进程可以对这一事实做出反应。当然，合约的任何功能都可以接收以太币，并且不受该气体限制的约束。如果函数不想接收任何以太币，它们实际上必须拒绝发送给它们的以太币，但我们正在考虑在未来的某个版本中可能会反转这种行为。