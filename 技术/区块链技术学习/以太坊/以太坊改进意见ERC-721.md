# 以太坊改进意见 ERC-721 
##  ERC-721
### 总结
不可替代令牌的标准接口，也称为契约。
###  ERC-721 抽象
以下标准允许在智能合约中为 NFT 实现标准 API。该标准提供了跟踪和传输 NFT 的基本功能。

我们考虑了个人拥有和交易 NFT 的用例，以及委托给第三方经纪人/钱包/拍卖人（“运营商”）的情况。NFT 可以代表对数字资产或实物资产的所有权。我们考虑了资产的多样化，并且我们知道您会梦想更多：

- 物产
	- 房屋
	- 独特的艺术品
- 虚拟收藏品
	- 小猫
	- 收藏卡
	- 独特图片
- “负值”资产
	- 贷款
	- 负债
	- 其他责任

总的来说，所有房屋都是不同的，没有两只小猫是一样的。NFT 是可区分的，您必须分别跟踪每一个的所有权。

### 动机
标准接口允许钱包/经纪人/拍卖应用程序与以太坊上的任何 NFT 一起使用。我们提供简单的 ERC-721 智能合约以及跟踪任意数量 NFT 的合约。其他应用程序将在下面讨论。

该标准的灵感来自 ERC-20 令牌标准，并建立自 EIP-20 创建以来的两年经验。EIP-20 不足以跟踪 NFT，因为 ERC-721 代表的每种资产都是不同的（不可替代），而 ERC-20 代表的每个代币的数量都是相同的（可替代）。

下面检查了该标准与 ERC-20 之间的差异。
###  规格
本文档中的关键字 “MUST”, “MUST NOT”, “REQUIRED”, “SHALL”, “SHALL NOT”, “SHOULD”, “SHOULD NOT”, “RECOMMENDED”, “MAY”, 和 “OPTIONAL” 是按照 RFC 2119 中的描述进行解释。
#### 每个符合 ERC-721 的合约都必须实现 ERC721 和 ERC165 接口,受下面的“警告”约束
	pragma solidity ^0.4.20;
	
	/// @title ERC-721 Non-Fungible Token Standard
	/// @dev See https://eips.ethereum.org/EIPS/eip-721
	///  Note: the ERC-165 identifier for this interface is 0x80ac58cd.
	interface ERC721 /* is ERC165 */ {
	    /// @dev This emits when ownership of any NFT changes by any mechanism.
	    ///  This event emits when NFTs are created (`from` == 0) and destroyed
	    ///  (`to` == 0). Exception: during contract creation, any number of NFTs
	    ///  may be created and assigned without emitting Transfer. At the time of
	    ///  any transfer, the approved address for that NFT (if any) is reset to none.
	    
	    /// @dev当任何 NFT 的所有权通过任何机制更改时发出。
	    /// 当创建 NFT（`from` == 0）并销毁（`to` == 0）时，
	    /// 将发出此事件。例外：创建合约期间，可以创建和分配任意数量的 NFT，
	    /// 而不会发出 Transfer。在进行任何转移时，该 NFT 的批准地址（如果有）将重置为无。
	    event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);
	
	    /// @dev This emits when the approved address for an NFT is changed or
	    ///  reaffirmed. The zero address indicates there is no approved address.
	    ///  When a Transfer event emits, this also indicates that the approved
	    ///  address for that NFT (if any) is reset to none.
	    
	    /// @dev当更改或确认 NFT 的批准地址时发出。
	    /// 零地址表示没有批准的地址。
          /// 发出“转移”事件时，这还表示该 NFT 的批准地址（如果有）被重置为“无”。
	    event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);
	
	    /// @dev This emits when an operator is enabled or disabled for an owner.
	    ///  The operator can manage all NFTs of the owner.
	    
	    /// @dev 在为所有者启用或禁用操作员时发出。
	    /// 操作员可以管理所有者的所有 NFT。
	    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);
	
	    /// @notice Count all NFTs assigned to an owner
	    /// @dev NFTs assigned to the zero address are considered invalid, and this
	    ///  function throws for queries about the zero address.
	    /// @param _owner An address for whom to query the balance
	    /// @return The number of NFTs owned by `_owner`, possibly zero
	    
	    /// @notice 计算分配给所有者的所有 NFT
	   /// @dev 分配给零地址的 NFT 被视为无效，并且此函数引发有关零地址的查询。
	   /// @param _owner 查询余额的地址
	   /// @return“ _owner” 拥有的 NFT 的数量，可能为零
	    function balanceOf(address _owner) external view returns (uint256);
	
	    /// @notice Find the owner of an NFT
	    /// @dev NFTs assigned to zero address are considered invalid, and queries
	    ///  about them do throw.
	    /// @param _tokenId The identifier for an NFT
	    /// @return The address of the owner of the NFT
	    
	    /// @notice 查找 NFT 的所有者
          /// @dev 分配给零地址的 NFT 被视为无效，并且查询
          /// 关于它们的确抛出。
          /// @param _tokenId NFT 的标识符
          /// @return NFT 所有者的地址
	    function ownerOf(uint256 _tokenId) external view returns (address);
	
	    /// @notice Transfers the ownership of an NFT from one address to another address
	    /// @dev Throws unless `msg.sender` is the current owner, an authorized
	    ///  operator, or the approved address for this NFT. Throws if `_from` is
	    ///  not the current owner. Throws if `_to` is the zero address. Throws if
	    ///  `_tokenId` is not a valid NFT. When transfer is complete, this function
	    ///  checks if `_to` is a smart contract (code size > 0). If so, it calls
	    ///  `onERC721Received` on `_to` and throws if the return value is not
	    ///  `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`.
	    /// @param _from The current owner of the NFT
	    /// @param _to The new owner
	    /// @param _tokenId The NFT to transfer
	    /// @param data Additional data with no specified format, sent in call to `_to`
	    

		/// @notice 将 NFT 的所有权从一个地址转移到另一个地址
		/// @dev抛出，除非 `msg.sender` 是当前所有者（已授权）
		/// 运算符，或此 NFT 的批准地址。 如果 `_from` 不是当前所有者，则抛出该异常。 如果 _to 为零地址，则抛出该异常。
		/// 如果 `_tokenId` 不是有效的 NFT，则抛出该异常。 转移完成后，
		/// 此函数检查 `_to` 是否为智能合约（代码大小> 0）。 
		/// 如果是这样的话，它在 _to 上调用 onERC721Received，
		/// 如果返回值不是 bytes4（keccak256（“ onERC721Received（address，address，uint256，bytes）”）））则抛出该异常。
		/// @param _from NFT 的当前所有者
		/// @param _to 新所有者
		/// @param _tokenId 要传输的 NFT
		/// @param data 没有指定格式的其他数据，在调用 _to 时发送 
	    function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes data) external payable;
	
	    /// @notice Transfers the ownership of an NFT from one address to another address
	    /// @dev This works identically to the other function with an extra data parameter,
	    ///  except this function just sets data to "".
	    /// @param _from The current owner of the NFT
	    /// @param _to The new owner
	    /// @param _tokenId The NFT to transfer
	    
	    	/// @notice 将 NFT 的所有权从一个地址转移到另一个地址
		/// @dev 这与带有附加数据参数的其他函数相同，除非此函数只是将数据设置为“”。
		/// @param _from NFT 的当前所有者
		/// @param _to 新所有者
		/// @param _tokenId 要传输的NFT
	    function safeTransferFrom(address _from, address _to, uint256 _tokenId) external payable;
	
	    /// @notice Transfer ownership of an NFT -- THE CALLER IS RESPONSIBLE
	    ///  TO CONFIRM THAT `_to` IS CAPABLE OF RECEIVING NFTS OR ELSE
	    ///  THEY MAY BE PERMANENTLY LOST
	    /// @dev Throws unless `msg.sender` is the current owner, an authorized
	    ///  operator, or the approved address for this NFT. Throws if `_from` is
	    ///  not the current owner. Throws if `_to` is the zero address. Throws if
	    ///  `_tokenId` is not a valid NFT.
	    /// @param _from The current owner of the NFT
	    /// @param _to The new owner
	    /// @param _tokenId The NFT to transfer
	    
		/// @notice 转让 NFT 的所有权-呼叫者负责
		/// 确认`_to`有能力接收 NFTS，否则可能永久丢失
		/// @dev抛出，除非 `msg.sender` 是该 NFT 的当前所有者，授权操作员或批准的地址。
		/// 如果 `_from` 不是当前所有者，则抛出该异常。 
		/// 如果 _to 为零地址，则抛出该异常。
		///如果 `_tokenId` 不是有效的NFT，则抛出该异常。
		/// @param _from NFT的当前所有者
		/// @param _to新所有者
		/// @param _tokenId要传输的NFT
	    function transferFrom(address _from, address _to, uint256 _tokenId) external payable;
	
	    /// @notice Change or reaffirm the approved address for an NFT
	    /// @dev The zero address indicates there is no approved address.
	    ///  Throws unless `msg.sender` is the current NFT owner, or an authorized
	    ///  operator of the current owner.
	    /// @param _approved The new approved NFT controller
	    /// @param _tokenId The NFT to approve
	    
		 /// @notice 更改或重新确认 NFT 的批准地址
		/// @dev 零地址表示没有批准的地址。
		/// 除非`msg.sender`是当前的 NFT 所有者或授权的，否则抛出
		/// 当前所有者的运算符。
		/// @param _approved 新批准的NFT控制器
		/// @param _tokenId 要批准的NFT
	    function approve(address _approved, uint256 _tokenId) external payable;
	
	    /// @notice Enable or disable approval for a third party ("operator") to manage
	    ///  all of `msg.sender`'s assets
	    /// @dev Emits the ApprovalForAll event. The contract MUST allow
	    ///  multiple operators per owner.
	    /// @param _operator Address to add to the set of authorized operators
	    /// @param _approved True if the operator is approved, false to revoke approval
	    
	      /// @notice 启用或禁用对第三方（“运营商”）的许可，以管理所有 msg.sender 的资产
		/// @dev发出 ApprovalForAll 事件。 合约必须允许每个所有者有多个运营商。
		/// @param _operator 要添加到授权操作员集中的地址
		/// @param _approved 如果批准了操作员，则为 True，否则为撤消批准
	    function setApprovalForAll(address _operator, bool _approved) external;
	
	    /// @notice Get the approved address for a single NFT
	    /// @dev Throws if `_tokenId` is not a valid NFT.
	    /// @param _tokenId The NFT to find the approved address for
	    /// @return The approved address for this NFT, or the zero address if there is none
	    
	    	/// @notice 获取单个 NFT 的批准地址
		/// @dev如果 `_tokenId` 不是有效的NFT，则抛出该异常。
		/// @param _tokenId 查找要批准的地址的 NFT
		/// @return 此NFT的批准地址，如果没有则为零
	    function getApproved(uint256 _tokenId) external view returns (address);
	
	    /// @notice Query if an address is an authorized operator for another address
	    /// @param _owner The address that owns the NFTs
	    /// @param _operator The address that acts on behalf of the owner
	    /// @return True if `_operator` is an approved operator for `_owner`, false otherwise
	    
	   	 /// @notice查询一个地址是否是另一个地址的授权操作员
		/// @param _owner 拥有 NFT 的地址
		/// @param _operator 代表所有者的地址
		/// @return 如果 `_operator` 是 `_owner` 的已批准运算符，则为 true，否则为 false
	    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
	}
	
	interface ERC165 {
	    /// @notice Query if a contract implements an interface
	    /// @param interfaceID The interface identifier, as specified in ERC-165
	    /// @dev Interface identification is specified in ERC-165. This function
	    ///  uses less than 30,000 gas.
	    /// @return `true` if the contract implements `interfaceID` and
	    ///  `interfaceID` is not 0xffffffff, `false` otherwise
	    
	    
	    /// @notice 查询合约是否实现了接口
		/// @param interfaceID 接口标识符，如 ERC-165 中所指定
		/// @dev 接口标识在 ERC-165 中指定。 此功能使用的气体少于 30,000。
		/// @return 如果合约实现了“ interfaceID” 并且 “ interfaceID” 不是0xffffffff，则返回“ true”，否则返回“ false”
	    function supportsInterface(bytes4 interfaceID) external view returns (bool);
#### 如果钱包/经纪人/拍卖应用程序接受安全转移，则必须实现钱包接口。    
	/// @dev Note: the ERC-165 identifier for this interface is 0x150b7a02.
	interface ERC721TokenReceiver {
	    /// @notice Handle the receipt of an NFT
	    /// @dev The ERC721 smart contract calls this function on the recipient
	    ///  after a `transfer`. This function MAY throw to revert and reject the
	    ///  transfer. Return of other than the magic value MUST result in the
	    ///  transaction being reverted.
	    ///  Note: the contract address is always the message sender.
	    /// @param _operator The address which called `safeTransferFrom` function
	    /// @param _from The address which previously owned the token
	    /// @param _tokenId The NFT identifier which is being transferred
	    /// @param _data Additional data with no specified format
	    /// @return `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`
	    ///  unless throwing
	    
	    
	    /// @notice 处理 NFT 的收据
	    /// @dev ERC721 智能合约在 “转移” 后在接收者上调用此函数。
		 /// 此函数可以抛出以还原和拒绝传输。
		 /// 返回非魔幻值必须导致交易被还原。
	     /// 注意：合约地址始终是消息发件人。
	     /// @param _operator 调用 `safeTransferFrom` 函数的地址
	     /// @param _from 先前拥有令牌的地址
	     /// @param _tokenId 正在传输的 NFT 标识符
	     /// @param _data 没有指定格式的其他数据
	     /// @return`bytes4（keccak256（“ onERC721Received（address，address，uint256，bytes）”）））`
	     ///除非抛出
	    function onERC721Received(address _operator, address _from, uint256 _tokenId, bytes _data) external returns(bytes4);
	}
#### 该元数据扩展是可选的 ERC-721 智能合约（见“警告”，下同）。这使您可以查询智能合约的名称以及 NFT 代表的资产的详细信息。
	/// @title ERC-721 Non-Fungible Token Standard, optional metadata extension
	/// @dev See https://eips.ethereum.org/EIPS/eip-721
	///  Note: the ERC-165 identifier for this interface is 0x5b5e139f.
	interface ERC721Metadata /* is ERC721 */ {
	    /// @notice A descriptive name for a collection of NFTs in this contract
	    
	    /// @notice 此合约中NFT集合的描述性名称
	    function name() external view returns (string _name);
	    /// @notice An abbreviated name for NFTs in this contract
		
	    /// @notice此合约中 NFT 的缩写名称
	    function symbol() external view returns (string _symbol);
	
	    /// @notice A distinct Uniform Resource Identifier (URI) for a given asset.
	    /// @dev Throws if `_tokenId` is not a valid NFT. URIs are defined in RFC
	    ///  3986. The URI may point to a JSON file that conforms to the "ERC721
	    ///  Metadata JSON Schema".
	    
		/// @notice 给定资产的不同统一资源标识符（URI）。
		/// @dev 如果`_tokenId` 不是有效的 NFT，则抛出该异常。 URI 在 RFC 3986 中定义。
		/// URI 可能指向符合“ ERC721 元数据 JSON 架构”的 JSON 文件。
	    function tokenURI(uint256 _tokenId) external view returns (string);
	}
#### 这是上面引用的“ ERC721元数据JSON架构”。
	{
	    "title": "Asset Metadata",
	    "type": "object",
	    "properties": {
	        "name": {
	            "type": "string",
	            "description": "Identifies the asset to which this NFT represents"
	        },
	        "description": {
	            "type": "string",
	            "description": "Describes the asset to which this NFT represents"
	        },
	        "image": {
	            "type": "string",
	            "description": "A URI pointing to a resource with mime type image/* representing the asset to which this NFT represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive."
	        }
	    }
	}

#### 对于 ERC-721 智能合约，枚举扩展名是可选的（请参见下面的“小标题”）。这使您的合约可以发布其 NFT 的完整列表并使其可被发现。
	/// @title ERC-721 Non-Fungible Token Standard, optional enumeration extension
	/// @dev See https://eips.ethereum.org/EIPS/eip-721
	///  Note: the ERC-165 identifier for this interface is 0x780e9d63.
	interface ERC721Enumerable /* is ERC721 */ {
	    /// @notice Count NFTs tracked by this contract
	    /// @return A count of valid NFTs tracked by this contract, where each one of
	    ///  them has an assigned and queryable owner not equal to the zero address
	    
	    /// @notice 此合约跟踪的 NFT 数量
		/// @return 此合约跟踪的有效 NFT 计数，其中每个 NFT 都有一个分配的且可查询的所有者，不等于ß零地址
	    function totalSupply() external view returns (uint256);
	
	    /// @notice Enumerate valid NFTs
	    /// @dev Throws if `_index` >= `totalSupply()`.
	    /// @param _index A counter less than `totalSupply()`
	    /// @return The token identifier for the `_index`th NFT,
	    ///  (sort order not specified)
	    
	    /// @notice 枚举有效的 NFT
		/// @dev 如果`_index`> =`totalSupply（）`抛出。
		/// @param _index 小于`totalSupply（）`的计数器
		/// @return 第 _index 个 NFT 的令牌标识符（未指定排序顺序）
	    function tokenByIndex(uint256 _index) external view returns (uint256);
	
	    /// @notice Enumerate NFTs assigned to an owner
	    /// @dev Throws if `_index` >= `balanceOf(_owner)` or if
	    ///  `_owner` is the zero address, representing invalid NFTs.
	    /// @param _owner An address where we are interested in NFTs owned by them
	    /// @param _index A counter less than `balanceOf(_owner)`
	    /// @return The token identifier for the `_index`th NFT assigned to `_owner`,
	    ///   (sort order not specified)
	    
	    /// @notice 枚举分配给所有者的 NFT
		/// @dev 如果 `_index`> =`balanceOf（_owner）` 或 `_owner` 为零地址（表示无效的NFT），则抛出该错误。
		/// @param _owner 我们对其拥有的 NFT 感兴趣的地址
		/// @param _index 小于 `balanceOf（_owner）` 的计数器
		/// @return 分配给 _owner 的第 _index 个 NFT 的令牌标识符（未指定排序顺序）
	    function tokenOfOwnerByIndex(address _owner, uint256 _index) external view returns (uint256);
	}
### 注意事项
0.4.20 Solidity 接口语法的表达能力不足以记录 ERC-721 标准。符合 ERC-721 的合约还必须遵守以下规定：

- 实体问题＃3412

	上述接口为每个功能包括明确的可变性保证。可变性的保证是，为了弱到强：`payable`、`implicit  nonpayable`、`view`和`pure`。您的实现必须满足此接口中的可变性保证，并且您可以满足更强的保证。例如，payable 此接口中的功能可以在您的合约中实现为非有效（未指定状态可变性）。我们期望更高版本的 Solidity 版本将允许您从该接口继承更严格的合约，但是对于 0.4.20 版本，一种变通方法是您可以编辑此接口以在从合约继承之前添加更严格的可变性。
- 实体问题＃3419

	已实施 `ERC721 Metadata`或 `ERC721 Enumerable` 也应实施的合约 `ERC721`。ERC-721 实现了接口 ERC-165 的要求。
- 实体问题＃2330

	如果某个功能在本规范中显示为，external 那么如果合约使用 `public` 可见性，则该合约将合规。作为版本 0.4.20 的解决方法，您可以编辑此界面以 public 在从合约继承之前切换到。
- Solidity问题＃3494，＃3544

	Solidity 的使用 `this.*.selector` 被标记为警告，Solidity 的未来版本不会将其标记为错误。

如果较新版本的 Solidity 允许在代码中表达警告，则可以更新此 EIP 并删除警告，这将等效于原始规范。

### 基本原理
以太坊智能合约的许多提议用途都依赖于跟踪可区分的资产。现有或计划中的 NFT 的例子包括 Decentraland 中的 LAND，CryptoPunk 中的同名朋克，以及使用 DMarket 或 EnjinCoin 等系统的游戏内物品。将来的用途包括跟踪现实世界的资产，例如房地产（如 Ubitquity 或 Propy 等公司所设想的。在每种情况下，至关重要的是，这些项目不要像账本中的数字那样“集中”在一起，而是每个资产无论这些资产的性质如何，如果我们拥有允许跨职能资产管理和销售平台的标准化接口，那么生态系统将变得更加强大。
#### ` NFT` 选词
`NFT` 几乎使所有接受调查的人都满意，并且广泛适用于可区分的数字资产的广泛领域。我们认识到，“契约” 对于该标准的某些应用（特别是物理属性）具有非常强的描述性。

考虑的替代方案：可区分的资产，所有权，代币，资产，股权，票据
#### NFT 标识符
每个 NFT 都由 `uint256` ERC-721 智能合约内的唯一ID标识。该标识号在合约有效期内不得更改。(contract address, uint256 tokenId) 然后，这对将成为以太坊链上特定资产的全球唯一且完全合格的标识符。尽管某些 ERC-721 智能合约可能会方便地以 ID 0 开头并为每个新的 NFT 加 1，但呼叫者不得假定 ID 号具有任何特定的模式，并且必须将 ID 视为“黑匣子”。另请注意，NFT 可能会失效（被销毁）。请参阅所支持的枚举接口的枚举功能。

`uint256` 由于 UUID 和 sha3 哈希可以直接转换，因此可以选择各种各样的应用程序 `uint256`。
#### 转移机制
ERC-721 标准化了安全传递函数 `safeTransferFrom`（带有和不带有 `bytes` 参数的重载）和不安全函数 `transferFrom`。转移可以通过以下方式发起：

- NFT 的所有者
- NFT 的批准地址
- NFT 当前所有者的授权运营商

此外，授权运营商可以为 NFT 设置批准的地址。这为钱包、经纪人和拍卖应用程序提供了一套功能强大的工具，可以快速使用大量 NFT。

传递和接受函数的文档仅在事务必须抛出时指定条件。您的实现也可能在其他情况下抛出。这使实现可以实现有趣的结果：

- 如果合约暂停，则禁止转移

	现有技术，CryptoKitties 部署的合约，第 611 行
- 将接收到的 NFT 中的某些地址列入黑名单

	现有技术，CryptoKitties 部署的合约，第 565、566 行
- 禁止进行不安全的传输

	`transferFrom` 抛出该异常，除非 `_to` 等于 `msg.sender` 或 `countOf(_to)` 非零或之前非零（因为这种情况是安全的）
- 收取费用的交易双方

	需要支付打电话时 `approve` 用非零 `_approved` 如果它以前的零个地址，退回货款，如果在调用 `approve` 与零个地址，如果它以前是一个非零地址，调用任何时候要求支付传递函数，要求传递参数 `_to` 相等 `msg.sender`，要求传递参数 `_to` 为 NFT 的批准地址
- 只读 NFT 注册表

	总是抛出 `unsafeTransfer`，`transferFrom`，`approve`和 `setApprovalForAll`
	
失败的事务将抛出，这是 ERC-223，ERC-677，ERC-827 和 OpenZeppelin 实施 SafeERC20.sol 中确定的最佳实践。ERC-20 定义了一个 allowance 功能，如 OpenZeppelin 问题＃438所示，这在调用并随后修改为不同的数量时引起了问题。在 ERC-721 中，没有配额，因为每个 NFT 都是唯一的，数量是一个或一个。因此，我们获得了 ERC-20 原始设计的好处，而没有后来发现的问题。

NFT（“铸造”）和销毁NFT（“燃烧”）的创建未包含在规范中。您的合约可以通过其他方式实施。event 创建或销毁 NFT 时，请参阅文档以了解您的职责。

我们询问是否需要 `operatoron` 参数 `onERC721Received`。在所有情况下，我们都可以想象，如果操作员很重要，那么该操作员可以将令牌转让给他们自己，然后将其发送-那么它们就是 from 地址。这似乎是人为的，因为我们认为操作员是令牌的临时所有者（并且将令牌转移给他们自己是多余的）。当操作员发送令牌时，该操作员是按照自己的意愿行事，而不是代表令牌持有者行事的操作员。这就是为什么操作员和先前的令牌所有者对令牌接收者都很重要的原因。

考虑的替代方案：仅允许两步 ERC-20 样式的事务，要求传递函数从不抛出，要求所有函数返回指示操作成功的布尔值。

#### ERC-165 接口
我们选择了标准接口检测（ERC-165）来公开 ERC-721 智能合约支持的接口。

未来的 EIP 可能会创建合约接口的全局注册表。我们坚决支持这样的 EIP，它将使您的 ERC-721 实施来实现 `ERC721Enumerable`，`ERC721Metadata` 通过委托给一个独立的合约或其他接口。
#### 复杂性（关于枚举扩展）
本规范考虑了管理少数几个任意数量的 NFT 的实现。如果您的应用程序能够增长，请避免在代码中使用 `for/while` 循环（请参阅 CryptoKitties 赏金问题＃4）。这些表明您的合约可能无法扩展，GAS 成本将随着时间的增长而不受限制。

我们已向 Testnet 部署了一个合约 XXXXERC721，该合约实例化并跟踪了340282366920938463463374374431768211456 不同的行为（2^128）。这就足以将每个 IPV6 地址分配给以太坊帐户所有者，或跟踪纳米机器人的所有权，而纳米机器人的大小只有几微米，总大小仅为地球的一半。您可以从区块链查询它。而且，每个函数比查询 ENS 所耗费的精力更少。

该图很清楚：ERC-721 标准秤。

考虑的替代方案：如果需要枚举，请删除资产枚举函数，然后从枚举函数返回 Solidity 数组类型。
#### 隐私
动机部分中确定的钱包/经纪人/拍卖人非常需要确定所有者拥有的 NFT。

考虑一个不能枚举 NFT 的用例，可能是有趣的，例如财产所有权的私有注册表或部分私有的注册表。但是，无法获得隐私，因为攻击者可以简单呼吁 `ownerOf` 一切可能 `tokenId`。
#### 元数据选择（元数据扩展）
我们需要 `name` 和 `symbol` 功能的扩展元数据。我们审查的每个令牌 EIP 和草案（ERC-20，ERC-223，ERC-677，ERC-777，ERC-827）都包含这些功能。

我们提醒实现作者是空字符串是一个有效的响应 `name`，`symbol` 如果您不同意这一机制的使用。

我们还提醒大家，任何智能合约都可以使用与您的合约相同的名称和符号。客户如何确定哪些 ERC-721 智能合约是众所周知的（规范的）不在本标准范围之内。

提供了一种将 NFT 与 URI 关联的机制。我们期望许多实现将利用此优势为每个 NFT 提供元数据。图像尺寸建议取自 Instagram，他们可能对图像的可用性了解很多。URI 可能是可变的（即，它会不时更改）。我们考虑了代表房屋所有权的 NFT，在这种情况下，有关房屋的元数据（图像，居住者等）可以自然改变。

元数据作为字符串值返回。目前，这仅可用于从调用 web3，而不能从其他合约调用。这是可以接受的，因为我们没有考虑过使用案例，在这种情况下，区块链上的应用程序会查询此类信息。

考虑的替代方案：将每种资产的所有元数据放在区块链上（太昂贵），使用 URL 模板查询元数据部分（URL 模板不适用于所有 URL 方案，尤其是 P2P URL），多地址网络地址（不够成熟）
#### 社区共识
关于原始 ERC-721 问题进行了大量讨论，此外，我们还举行了关于 Gitter 的首次现场会议，该会议具有良好的代表性和良好的宣传（在Reddit上的Gitter #ERC频道，以及原始的ERC-721问题）。谢谢参加者：

- DEC Gaming的@ImAllInNow Rob /演示密歇根以太坊聚会2月7日
- @亚历克斯·尼克·约翰逊
- @jadhavajay来自AyanWorks的Ajay Jadhav
- @superphly Cody Marx Bailey-XRAM资本/在黑客马拉松上分享1月20日/联合国金融未来黑客马拉松。
- @fulldecent威廉·恩崔肯

第二次活动在 ETHDenver 2018 举行，讨论可区分的资产标准（即将发布的注释）。

在此过程中，我们一直具有包容性，并邀请任何有疑问或贡献的人参与我们的讨论。但是，编写此标准仅是为了支持此处列出的确定的用例。

#### 向后兼容
我们已经通过 `balanceOf`，`totalSupply`，`name` 和 `symbol` 从 ERC-20 规范的语义。如果实现的目标是在支持该标准的同时与 ERC-20 更兼容，则该实现也可以包含 `decimals` 返回的功能 `uint8(0)`。但是，我们发现它要求所有 ERC-721 实现都支持该 `decimals` 功能。

截至2018年2月的 NFT 实施示例：

- CryptoKitties 

	与该标准的早期版本兼容。
- CryptoPunks 

	部分与 ERC-20 兼容，但不易推广，因为它直接在合约中包含拍卖功能，并使用将资产明确称为 “ punks” 的功能名称。
- 拍卖行资产界面

	作者需要一个通用的拍卖行 ÐApp 接口（目前是冰盒式的）。他的“资产”合约非常简单，但是缺少 ERC-20 的兼容性，approve() 功能和元数据。在 EIP-173 的讨论中引用了此工作。

注意：“限量版，可收藏代币”（例如 Curio 卡和 Rare Pepe ）不是可区分的资产。它们实际上是单个可替代令牌的集合，每个令牌均由其自己的智能合约以其自己的总供应量进行跟踪（在极端情况下可能如此）。

该 `onERC721Received` 函数专门用于解决已部署的旧合约，这些合约 `true` 在某些情况下可能会无意中返回1（），即使它们未实现功能（请参见 Solidity DelegateCallReturnValue 错误）。通过返回并检查魔术值，我们能够区分实际肯定的响应与这些虚假 `true` 的响应。

### 测试用例
0xcert ERC-721 令牌包括使用 Truffle 编写的测试用例。
#### 测试实例参考
- 0xcert ERC721  参考实施
	- 麻省理工学院许可，因此您可以在项目中自由使用它
	- 包括测试用例
	- 活动的赏金赏金，如果发现错误，您将获得报酬
- Su Squares  一个广告平台，您可以在其中租用空间并放置图片
	- 完成 Su Squares Bug 赏金计划，以查找有关此标准或其实施的问题
	- 实现完整的标准和所有可选接口
- ERC721ExampleDeed –示例实现
	- 使用 OpenZeppelin 项目格式实施
- William Entriken 撰写的 XXXXERC721 –可扩展的示例实现
	- 部署在拥有 10 亿资产的 testnet 上，并通过元数据扩展支持所有查找。这表明缩放不是问题。

#### 参考
- 标准品
	- ERC-20 令牌标准 https://eips.ethereum.org/EIPS/eip-20
	- ERC-165 标准接口检测 https://eips.ethereum.org/EIPS/eip-165
	- ERC-173 拥有的标准 https://eips.ethereum.org/EIPS/eip-173
	- ERC-223 令牌标准 https://github.com/ethereum/EIPs/issues/223
	- ERC-677 transferAndCall 令牌标准 https://github.com/ethereum/EIPs/issues/677
	- ERC-827 令牌标准 https://github.com/ethereum/EIPs/issues/827
	- 以太坊名称服务（ENS）https://ens.domains
	- Instagram –什么是图像分辨率？https://help.instagram.com/1631821640426723
	- JSON模式。https://json-schema.org/
	- Multiaddr。https://github.com/multiformats/multiaddr
	- RFC 2119在RFC中用于指示需求级别的关键字。https://www.ietf.org/rfc/rfc2119.txt
- 问题
	- ERC-721原始版本。https://github.com/ethereum/eips/issues/721
	- Solidity Issue＃2330 –接口功能是外部的。https://github.com/ethereum/solidity/issues/2330
	- Solidity问题＃3412 –实施接口：允许更严格的可变性。https://github.com/ethereum/solidity/issues/3412
	- 实体问题＃3419-接口无法继承。https://github.com/ethereum/solidity/issues/3419
	- Solidity问题＃3494 –编译器关于该selector函数的错误原因。https://github.com/ethereum/solidity/issues/3494
	- Solidity问题＃3544 –无法计算名为的函数的选择器transfer。https://github.com/ethereum/solidity/issues/3544
	- CryptoKitties赏金问题＃4-列出用户拥有的所有小猫O(n^2)。https://github.com/axiomzen/cryptokitties-bounty/issues/4
	- OpenZeppelin问题＃438 –approve方法的实现违反了ERC20标准。https://github.com/OpenZeppelin/zeppelin-solidity/issues/438
	- Solidity DelegateCallReturnValue错误。https://solidity.readthedocs.io/zh-CN/develop/bugs.html#DelegateCallReturnValue
- 讨论区
	- Reddit（首次现场讨论的公告）。https://www.reddit.com/r/ethereum/comments/7r2ena/friday_119_live_discussion_on_erc_nonfungible/
	- Gitter #EIP（首次现场讨论的公告）。https://gitter.im/ethereum/EIPs?at=5a5f823fb48e8c3566f0a5e7
	- ERC-721（首次现场讨论的公告）。https://github.com/ethereum/eips/issues/721#issuecomment-358369377
	- ETHDenver 2018.https：//ethdenver.com
- NFT实践和项目
	- CryptoKitties。https://www.cryptokitties.co
	- 0xcert ERC-721令牌。https://github.com/0xcert/ethereum-erc721
	- 苏广场。https://tenthousandsu.com
	- Decentraland。https://decentraland.org
	- 加密朋克。https://www.larvalabs.com/cryptopunks
	- DMarket。https://www.dmarket.io
	- 恩金币 https://enjincoin.io
	- 无所不在。https://www.ubitquity.io
	- Propy。https://tokensale.propy.com
	- CryptoKitties部署合同。https://etherscan.io/address/0x06012c8cf97bead5deae237070f9587f8e7a266d#code
	- Su Squares Bug赏金计划。https://github.com/fulldecent/su-squares-bounty
	- XXXXERC721。https://github.com/fulldecent/erc721-example
	- ERC721ExampleDeed。https://github.com/nastassiasachs/ERC721ExampleDeed
	- 古玩卡。https://mycuriocards.com
	- 稀有的佩佩。https://rarepepewallet.com
	- 拍卖行资产界面。https://github.com/dob/auctionhouse/blob/master/contracts/Asset.sol
	- OpenZeppelin SafeERC20.sol实施。https://github.com/OpenZeppelin/zeppelin-solidity/blob/master/contracts/token/ERC20/SafeERC20.sol
 
###  版权
通过CC0放弃版权和相关权。

### 引文
请将此文档引用为：

William Entriken，Dieter Shirley，Jacob Evans和Nastassia Sachs，“ EIP-721：ERC-721不可替代令牌标准”，以太坊改进提案，第1号。721，2018年1月。[在线丛书]。可用：https：//eips.ethereum.org/EIPS/eip-721。

## 参考
- [eip-721](https://eips.ethereum.org/EIPS/eip-721)
- [ERC-721](http://erc721.org)